Actor Doomguy_Perk1 : DnD_Boolean { }
Actor Doomguy_CanExecute : DnD_Boolean { }
Actor Doomguy_ChangedColor : DnD_Boolean { }
Actor Doomguy_ValidExecute : DnD_Boolean { }

Actor Doomguy_Perk20 : DnD_Boolean { }

Actor Doomguy_Perk20_Damage : PowerDamage {
	damagefactor "normal", 1.0
	Powerup.Color "DF 11 11", 0.2
	powerup.duration -10
}

Actor Doomguy_Perk20_Damage_Execute : PowerDamage {
	damagefactor "normal", 1.0
	Powerup.Color "DF 11 11", 0.2
	powerup.duration -20
}

Actor Doomguy_Perk40 : DnD_Boolean { }
Actor Doomguy_Perk60 : DnD_Boolean { }
Actor Doomguy_Perk80 : DnD_Boolean { }

Actor Doomguy_DemonSoul : DnD_DroppedItem {
	Radius 16
	Height 24
	Scale 0.75
	+FLOATBOB
	
	States {
		Spawn:
		SpawnLoop:
			DG50 AAAABBBBCCCCDDDDCCCCBBBB 1 Bright A_LookEx(LOF_NOSOUNDCHECK, 0, 28, 0, 360, "CheckPickup")
		Loop
		
		See:
		CheckPickup:
			"####" "#" 0 A_JumpIfInventory("TaltosUp", 1, "SpawnLoop")
			"####" "#" 1 A_JumpIfCloser(32, "Pickup")
		goto SpawnLoop

		Pickup:
			TNT1 A 0 A_GiveToTarget("Doomguy_DemonSoul_PickMsg", 1)
			TNT1 A 0 A_GiveToTarget("Doomguy_Perk20_Damage", 1)
			TNT1 A 0 ACS_NamedExecuteAlways("DnD Doomguy Soul Pickup", 0, mass)
			TNT1 A 3
		stop
	}
}

Actor Doomguy_DemonSoul_PickMsg : CustomInventory {
	States {
		Pickup:
			TNT1 A 0 ACS_NamedExecuteAlways("DnD Message Logger", 0, 4)
			TNT1 A 0 A_PlaySound("Items/DoomguySoulPick", 6)
			TNT1 A 0 A_SetBlend("60 12 12", 0.6, 17)
		Stop
	}
}

Actor Doomguy_ResistReduced : PowerDamage {
	damagefactor "normal", 1.0
	powerup.duration -5
}

Actor Marine_Perk1 : DnD_Boolean { }
Actor Marine_Perk20 : DnD_Boolean { }
Actor Marine_Perk40 : DnD_Boolean { }
Actor Marine_Perk60 : DnD_Boolean { }
Actor Marine_Perk80 : DnD_Boolean { }

Actor Marine_DamageReduction_Timer : DnD_Info {
	inventory.maxamount 525
}

Actor Marine_Perk50_Cooldown : DnD_Info {
	inventory.maxamount 6300
}

Actor Marine_Perk50_DamageDealt : DnD_Info { }
Actor Marine_Perk50_DamageTaken : DnD_Info { }

Actor Hobo_Perk1 : DnD_Boolean { }
Actor Hobo_Perk20 : DnD_Boolean { }
Actor Hobo_Perk40 : DnD_Boolean { }
Actor Hobo_Perk60 : DnD_Boolean { }
Actor Hobo_Perk80 : DnD_Boolean { }

Actor DnD_HasAmphetamine : DnD_Boolean { }
Actor NoMoreAmphetamine : PowerDamage {
	damagefactor "normal", 1.0
	powerup.duration 0x7FFFFFFF
}

ACTOR SpentSyringe {
	Scale 0.1
	Radius 3
	Height 3
	Speed 10
	Mass 5
	
	+NOBLOCKMAP 
	+DROPOFF 
	+MISSILE 
	+NOTELEPORT 
	+FLOORCLIP 
	+DOOMBOUNCE
	+CLIENTSIDEONLY
	
	SeeSound "Weapons/shotguncasing"
	States {
		Spawn:
			SYTR ABCDEFGHIJKLMNOP 3
		Loop
		Death:
			SYTR A -1
		Stop
	}
}

Actor Hobo_ShotgunTag : Inventory {
	inventory.maxamount 4
}

Actor Hobo_ShotgunTag_Window : PowerProtection {
	damagefactor "normal", 1.0
	powerup.duration -10
}

Actor Hobo_ShotgunFrenzyTimer : DnD_Info {
	inventory.maxamount 700 // default setup is 350 = 10 secs
}

Actor Punisher_Perk1 : DnD_Boolean { }

Actor Punisher_Perk1_MoveSpeed : PowerSpeed {
	powerup.duration 0x7FFFFFFF
	Speed 1.1
}

Actor Punisher_Perk20 : DnD_Boolean { }
Actor Punisher_Perk40 : DnD_Boolean { }
Actor Punisher_Perk60 : DnD_Boolean { }
Actor Punisher_Perk80 : DnD_Boolean { }

Actor Punisher_Perk50_Counter : Inventory {
	inventory.maxamount 0x7FFFFFFF
}

Actor Punisher_Perk50_Tiers : Inventory {
	inventory.maxamount 10
}

Actor Punisher_Perk50_DamageBonus : Inventory {
	inventory.maxamount 0x7FFFFFFF
}

Actor Punisher_Perk50_NextReq : Inventory {
	inventory.maxamount 0x7FFFFFFF
}

Actor Wanderer_Perk1 : DnD_Boolean { }
Actor Wanderer_Perk20 : DnD_Boolean { }
Actor Wanderer_Perk40 : DnD_Boolean { }
Actor Wanderer_Perk60 : DnD_Boolean { }
Actor Wanderer_Perk80 : DnD_Boolean { }

Actor NoMoreEtherealForm : PowerDamage {
	damagefactor "normal", 1.0
	powerup.duration 0x7FFFFFFF
}

Actor Wanderer_Explosion : DnD_ExplosiveBase {
	Renderstyle "Add"
	Height 1
	Radius 1
	Speed 0
	Stamina 2097152
	+THRUACTORS
	
	var int user_dmg;
	var int user_damagetype;
	
	States {
		SpawnState:
			TNT1 A 0
			"####" A 0 A_SetUserVar("user_expflags", DND_DAMAGEFLAG_NONWEAPON | DND_DAMAGEFLAG_ISRADIUSDMG)
		Death:
			TNT1 A 1 A_PlaySound("WandererExplosion")
			TNT1 A 0 A_SetArg(0, ACS_NamedExecuteWithResult("DnD Scale Non Weapon Damage", user_dmg, user_damagetype, user_expflags))
			TNT1 A 0 A_Jump(256, "Fire", "Poison", "Lightning", "Ice")
		Fire:
			FXPO A 0 A_SetArg(0, Args[0] | (DND_DAMAGETYPE_FIRE << NONWEP_DMG_SHIFT))
		Goto DoDmg
		Poison:
			FXPG A 0 A_SetArg(0, Args[0] | (DND_DAMAGETYPE_POISON << NONWEP_DMG_SHIFT))
		Goto DoDmg
		Lightning:
			FXPY A 0 A_SetArg(0, Args[0] | (DND_DAMAGETYPE_LIGHTNING << NONWEP_DMG_SHIFT))
		Goto DoDmg
		Ice:
			FXPB A 0 A_SetArg(0, Args[0] | (DND_DAMAGETYPE_ICE << NONWEP_DMG_SHIFT))
		Goto DoDmg
		DoDmg:
			"####" A 0 A_SetUserVar("user_expdmg", Args[0])
			"####" A 0 A_SetUserVar("user_expradius", 160)
			"####" A 0 A_SetUserVar("user_fullexpradius", user_expradius)
		Goto DoExplosionDamage
		ContinueFromExplosion:
			"####" A 0 A_ChangeVelocity(frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(0.33, 0.75), CVF_REPLACE)
			"####" AB 2 Bright
			"####" C 2 Bright
			"####" DEFGHIJKLM 2 Bright
		Stop
	}
}

Actor DnD_AilmentToken : DnD_Info {
	inventory.maxamount 64
}

Actor Wanderer_Ascended : DnDWeapon {
	Weapon.Slotnumber 0
	Weapon.SelectionOrder 1
	+INVENTORY.UNDROPPABLE
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 1.6
	Weapon.BobRangeY 0.2
	Weapon.BobRangeX 0.4
	Weapon.AmmoUse 0
	Weapon.AmmoGive 0
	Tag "$WEP_SPECIAL0_TAG"
	States {
		Spawn:
			TNT1 A -1
		Stop
		Ready:
			TNT1 A 0 ACS_NamedExecuteWithResult("DnD Weapon Select", DND_WEAPON_ASCENSION)
			TNT1 A 0 ACS_NamedExecuteWithResult("DND Weapon Damage Cache", DND_WEAPON_ASCENSION)
			TNT1 A 0 ACS_NamedExecuteWithResult("DnD Hud Animation", DND_ANIM_WANDERERSPARKLES)
			TNT1 A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH) // fixes a bad offset problem
			ETWF GFEDCBA 3 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH | WRF_NOBOB)
		ReadyLoop:
			ETHF A 3 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOSWITCH | WRF_ALLOWZOOM)
		Loop
		Select:
			TNT1 A 0
		SelectLoop:
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
			TNT1 A 1 A_Raise
		Loop
		Deselect:
			TNT1 A 0 A_PlaySound("Wanderer/RayFinish", 7)
		DeselectLoop:
			ETWF ABCDEFG 3
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
			TNT1 A 1 A_Lower
		Loop
		
		// Ray
		Fire:
			ETHF BCDE 2
		FireHold:
			TNT1 A 0 A_PlaySound("Wanderer/RayLoop", 7, 1.0, 1)
			ETHF T 1 Offset(1, 33)
			ETHF T 1 Offset(2, 34)
			TNT1 A 0 ACS_NamedExecuteWithResult("DnD Player Buff", BTI_KILLSTORM)
			ETHF T 1 Offset(4, 36)
		FireLoop:
			TNT1 A 0 A_Jump(80, "FireAnim1", "FireAnim1", "FireAnim1", "FireAnim1", "FireAnim2", "FireAnim2", "FireAnim2", "FireAnim3", "FireAnim3")
			ETHF F 1 Bright Offset(4, 36) ACS_NamedExecuteWithResult("DnD Ray of Disintegration Trails")
		Goto FireFinish
		FireAnim1:
			ETHF F 1 Bright Offset(5, 37) ACS_NamedExecuteWithResult("DnD Ray of Disintegration Trails")
		Goto FireFinish
		FireAnim2:
			ETHF F 1 Bright Offset(6, 38) ACS_NamedExecuteWithResult("DnD Ray of Disintegration Trails")
		Goto FireFinish
		FireAnim3:
			ETHF F 1 Bright Offset(7, 39) ACS_NamedExecuteWithResult("DnD Ray of Disintegration Trails")
		Goto FireFinish
		
		FireFinish:
			TNT1 A 0 A_GiveInventory("WandererRayShooter", 1)
			ETHF F 1 Bright Offset(4, 36)
			ETHF F 1 Bright Offset(2, 34) A_Refire("FireLoop")
			TNT1 A 0 A_PlaySound("Wanderer/RayFinish", 7)
			TNT1 A 0 ACS_NamedExecuteWithResult("DnD Player Buff", BTI_KILLSTORM, BTI_F_REMOVE)
			ETHF EDCB 2 Offset(0, 32) A_Refire("FireHold")
		Goto ReadyLoop
		
		// Slam
		AltFire:
			ETHF A 1 A_JumpIfInventory("Wanderer_BlastCooldown", 1, "FailAlt")
			ETHF G 1
			ETHF G 2 A_PlaySound("Wanderer/Charge", CHAN_6)
			ETHF H 3
			ETHF IJ 3
			ETHF K 3 A_FireCustomMissile("WandererShockwaveFX")
			ETHF K 1 Offset(-1, 33)
			ETHF K 1 Offset(-2, 34)
			ETHF K 1 Offset(-3, 36)
			ETHF K 1 Offset(-5, 38)
			ETHF K 1 Offset(-6, 39)
			ETHF K 1 Offset(-7, 41)
			ETHF K 1 Offset(-10, 43)
			ETHF K 1 Offset(-13, 46)
			ETHF K 3 Offset(-15, 48)
			TNT1 A 0 A_PlaySound("Wanderer/Blast", CHAN_7)
			ETHF K 1 Offset(-16, 47)
			ETHF K 1 Offset(-14, 49)
			ETHF K 1 Offset(-15, 48)
			ETHF K 1 Offset(-14, 47)
			ETHF K 1 Offset(-16, 48)
			ETHF K 1 Offset(-15, 47)
			ETHF K 1 Offset(-16, 49)
			ETHF K 1 Offset(-14, 48)
			ETHF K 1 Offset(-16, 47)
			ETHF K 1 Offset(-14, 49)
			ETHF K 1 Offset(-15, 48)
			ETHF K 1 Offset(-14, 47)
			ETHF K 1 Offset(-16, 48)
			ETHF K 1 Offset(-15, 47)
			ETHF K 1 Offset(-16, 49)
			ETHF K 1 Offset(-14, 48)
			ETHF K 1 Offset(-16, 47)
			ETHF K 1 Offset(-14, 49)
			ETHF K 1 Offset(-15, 48)
			ETHF K 1 Offset(-14, 47)
			ETHF K 1 Offset(-16, 48)
			ETHF K 1 Offset(-15, 47)
			ETHF K 1 Offset(-16, 49)
			ETHF K 1 Offset(-14, 48)
			ETHF K 1 Offset(-16, 47)
			ETHF K 1 Offset(-14, 49)
			ETHF K 1 Offset(-15, 48)
			ETHF K 1 Offset(-14, 47)
			ETHF K 1 Offset(-16, 48)
			ETHF K 1 Offset(-15, 47)
			ETHF K 1 Offset(-16, 49)
			ETHF K 1 Offset(-14, 48)
			ETHF K 1 Offset(-10, 43)
			ETHF K 1 Offset(-6, 39)
			ETHF K 1 Offset(-3, 36)
			ETHF K 1 Offset(-2, 34)
			ETHF K 1 Offset(-1, 33)
			TNT1 A 0 A_GiveInventory("Wanderer_ShockwaveTossSignal", 1)
			TNT1 A 0 A_FireCustomMissile("WandererShockwave_Real")
			ETHF LMNOP 3
			TNT1 A 36
			TNT1 A 0 A_GiveInventory("Wanderer_BlastCooldown", 1)
			ETHF SRQ 2
		Goto ReadyLoop
		FailAlt:
			ETHF A 3 A_WeaponReady(WRF_NOSECONDARY | WRF_ALLOWZOOM | WRF_ALLOWRELOAD | WRF_NOSWITCH)
		Goto ReadyLoop
		
		// Teleport
		Reload:
			ETHF A 1 A_JumpIfInventory("Wanderer_DashCooldown", 1, "FailAlt")
			ETHF Q 3
			TNT1 A 0 A_FireCustomMissile("Wanderer_DashDestination")
			TNT1 A 0 A_GiveInventory("Wanderer_DashSpeedFX", 1)
			TNT1 A 0 A_GiveInventory("Wanderer_DashCooldown", 1)
			TNT1 A 0 ACS_NamedExecuteWithResult("DnD Wanderer Dash")
			ETHF RRRSSS 1 A_SpawnItemEx("Wanderer_DashFX", 0, 0, 32)
			TNT1 AAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("Wanderer_DashFX", 0, 0, 32)
			TNT1 A 0 A_FireCustomMissile("Wanderer_DashDestination")
			ETHF SRQ 3
		Goto ReadyLoop
		
		RiseUpSpecial:
			ETHF A 1 Offset(110, 142) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE | WRF_DISABLESWITCH)
			ETHF A 1 Offset(90, 122) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE | WRF_DISABLESWITCH)
			ETHF A 1 Offset(60, 92) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE | WRF_DISABLESWITCH)
			ETHF A 1 Offset(30, 62) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE | WRF_DISABLESWITCH)
			ETHF A 1 Offset(15, 44) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE | WRF_DISABLESWITCH)
			ETHF A 0 Offset(0, 32) A_WeaponReady(WRF_NOBOB | WRF_NOFIRE | WRF_DISABLESWITCH)
		Goto Ready
	}
}

Actor Wanderer_ReturnCircle {
	Renderstyle Add
	Alpha 0.8
	Scale 3.5
	
	const float r = 40;
	const float rate = 0.5;
	const int max_amt = 360 / rate;
	
	var int user_count;
	+CLIENTSIDEONLY
	+NOINTERACTION
	States {
		Spawn:
			TNT1 A 1 NoDelay A_SetUserVar("user_count", 0)
		RealLoop:
			TNT1 A 0 A_JumpIfInTargetInventory("DnD_Boolean", 1, "Fade")
			TNT1 A 0 A_SetUserVar("user_count", (user_count + 1) % max_amt)
			AURA C 1 Bright A_Warp(AAPTR_TARGET, 0, 0, 0, 0, WARPF_INTERPOLATE | WARPF_USECALLERANGLE | WARPF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("PurpleParticleSlowVanish", r * cos(user_count * rate), r * sin(user_count * rate), 0, 0, 0, frandom(0.75, 1.25))
			TNT1 A 0 A_SpawnItemEx("PurpleParticleSlowVanish", r * cos(user_count * rate + 180), r * sin(user_count * rate + 180), 0, 0, 0, frandom(0.75, 1.25))
			TNT1 A 0 A_SetUserVar("user_count", (user_count + 1) % max_amt)
			AURA C 1 Bright A_Warp(AAPTR_TARGET, 0, 0, 0, 0, WARPF_INTERPOLATE | WARPF_USECALLERANGLE | WARPF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("PurpleParticleSlowVanish", r * cos(user_count * rate), r * sin(user_count * rate), 0, 0, 0, frandom(0.75, 1.25))
			TNT1 A 0 A_SpawnItemEx("PurpleParticleSlowVanish", r * cos(user_count * rate + 180), r * sin(user_count * rate + 180), 0, 0, 0, frandom(0.75, 1.25))
		Loop
		Fade:
			AURA CCCCCC 1 A_FadeOut(0.125)
		Stop
	}
}

Actor Wanderer_ReturnIndicator {
	Renderstyle Translucent
	Alpha 0.80
	Height 8
	Radius 4
	DamageType "Telefrag"
	+ISMONSTER
	-COUNTKILL
	+THRUACTORS
	+NOINTERACTION
	+FORCERADIUSDMG
	+NODAMAGETHRUST
	+FRIENDLY
	States {
		Spawn:
			TNT1 A 0
			TNT1 A 1 ACS_NamedExecuteWithResult("DnD Wanderer Return Circle")
		KeepLoop:
			MNK1 A 7
			MNK1 AAA 1 A_JumpIfInTargetInventory("Wanderer_Ascended", 1, "KeepLoop")
			TNT1 A 0 A_SpawnItemEx("DissipationEffect")
			TNT1 A 0 ACS_NamedExecuteAlways("DnD Wanderer Circle Signal", 0)
			TNT1 A 0 A_Explode(0x7FFFFFFF, 18, 0, 0, 18)
			MNK1 AAAAAAAAAA 1 A_FadeOut(0.1)
		Stop
	}
}

Actor Wanderer_DashCooldown : PowerProtection {
	powerup.duration -4
	damagefactor "normal", 1.0
}

Actor Wanderer_DashSpeedFX : PowerSpeed {
	powerup.duration 24
	Speed 1.0
}

Actor Wanderer_DashDestination : DnD_ExplosiveBase {
	PROJECTILE
	Height 1
	Radius 1
	Stamina 4194304
	+THRUACTORS
	+BLOODLESSIMPACT
	+FOILINVUL
	+FORCERADIUSDMG
	Alpha 0.2
	Renderstyle add
	States {
		Spawn:
			TNT1 A 0
		Death:
			TNT1 A 0 A_SetUserVar(
				"user_expdmg", 
				DND_WEAPON_ASCENSION |
				(DND_DMGID_2 << CACHE_SHIFT) |
				(DND_DAMAGETYPE_OCCULT << DTYPE_SHIFT) |
				(100 << DPCT_SHIFT)
			)
			TNT1 A 0 A_SetUserVar("user_expradius", 192 * (1.0 + ACS_NamedExecuteWithResult("DND Explosion Radius Retrieve", DND_USETARGET) * exprad_factor))
			TNT1 A 0 A_SetUserVar("user_fullexpradius", 80)
			TNT1 A 0 A_SetUserVar("user_expflags", 0)
		Goto DoExplosionDamage
		ContinueFromExplosion:
			NB03 DDDDDDD 1 Bright A_FadeIn(0.1)
			NB03 DDDDDDD 1 Bright A_FadeOut(0.1)
		Stop
	}
}

Actor Wanderer_DashDestination_Spawner : DnD_Activator {
	States {
		Pickup:
			TNT1 A 0 A_FireCustomMissile("Wanderer_DashDestination")
		Stop
	}
}

Actor Wanderer_DashFX {
	RenderStyle Add
	Alpha 0.9
	Scale 0.58
	+CLIENTSIDEONLY
	+NOINTERACTION
	States {
		Spawn:
			TNT1 A 0
			TNT1 A 1 Thing_ChangeTID(0, SPECIAL_FX_TID)
			TNT1 A 0 A_Jump(128, "Other")
			GSMX A 0
		Anim:
			"####" "#" 1 A_FadeOut(0.0875)
			"####" "#" 0 A_SetScale(scalex - 0.0875, scaley - 0.0875)
			"####" "#" 0 A_ChangeVelocity(0, 0, 2)
		Loop
		Other:
			GSMX B 0
		Goto Anim
	}
}

Actor Wanderer_ShockwaveTossSignal : PowerDamage {
	damagefactor "normal", 1.0
	powerup.duration 10
}

// spawn trails with acs instead purely decorative
const int wanderer_rayoffset = 15;
Actor WandererRayShooter : DnD_Activator {
	States {
		Pickup:
			// fire the 4 side rails to "extend" hitbox
			TNT1 A 0 A_SetArg(DND_ARG_DAMAGE,
				DND_WEAPON_ASCENSION |
				(DND_DMGID_0 << CACHE_SHIFT) |
				(DND_DAMAGETYPE_OCCULT << DTYPE_SHIFT) |
				(100 << DPCT_SHIFT)
			)
			TNT1 A 0 A_FireBullets(0, 0, 1, 0, "WandererRayPuff", 0, 1040)
			TNT1 A 0 A_RailAttack(Args[DND_ARG_DAMAGE], -wanderer_rayoffset, 0, none, none, RGF_SILENT, 0, "WandererRayPuff_Info", 0, 0, 1040, 0, 5.0, 0, none, wanderer_rayoffset + 1)
			TNT1 A 0 A_RailAttack(Args[DND_ARG_DAMAGE], -wanderer_rayoffset, 0, none, none, RGF_SILENT, 0, "WandererRayPuff_Info", 0, 0, 1040, 0, 5.0, 0, none, -wanderer_rayoffset + 1)
			TNT1 A 0 A_RailAttack(Args[DND_ARG_DAMAGE], wanderer_rayoffset, 0, none, none, RGF_SILENT, 0, "WandererRayPuff_Info", 0, 0, 1040, 0, 5.0, 0, none, wanderer_rayoffset + 1)
			TNT1 A 0 A_RailAttack(Args[DND_ARG_DAMAGE], wanderer_rayoffset, 0, none, none, RGF_SILENT, 0, "WandererRayPuff_Info", 0, 0, 1040, 0, 5.0, 0, none, -wanderer_rayoffset + 1)
			TNT1 A 0 A_RailAttack(Args[DND_ARG_DAMAGE], 0, 0, none, none, RGF_SILENT, 0, "WandererRayPuff_Info", 0, 0, 1040, 0, 5.0, 0, none, 1)
		Stop
	}
}

Actor WandererRayTrail {
	+NOINTERACTION
	+CLIENTSIDEONLY
	Height 16
	Radius 8
	Renderstyle Add
	Alpha 0.9
	Scale 0.425
	States {
		Spawn:
			DRAY A 1 Bright A_FadeOut(0.1275)
		Loop
	}
}

Actor WandererRayPuff_Info : DnD_BaseRailPuff {
	Stamina 4096
	+FOILINVUL
	States {
		Spawn:
		Crash:
		HitNoBlood:
		Melee:
			TNT1 AAAA 0
			TNT1 A 1
		Stop
	}
}

Actor WandererRayPuff {
	Alpha 0.6
	RenderStyle Add
	Scale 0.275
	Height 16
	Radius 8
	+ALWAYSPUFF
	+THRUACTORS
	+CLIENTSIDEONLY
	States {
		Spawn:
		Crash:
			TNT1 AAAAAAA 0
		SpawnLoop:
			FLAR D 1 Bright A_FadeOut(0.0825)
		Loop
	}
}

Actor WandererShockwaveFX {
	Renderstyle Add
	Alpha 0.95
	+MISSILE
	+THRUACTORS
	+NOCLIP
	+NOINTERACTION
	+CLIENTSIDEONLY
	States {
		Spawn:
			TNT1 A 0 A_JumpIfInTargetInventory("Wanderer_ShockwaveTossSignal", 1, "GoAway")
			ETHP AAB 1 Bright A_Warp(AAPTR_TARGET, 32, 0, 28, 0, WARPF_INTERPOLATE)
			TNT1 A 0 A_JumpIfInTargetInventory("Wanderer_ShockwaveTossSignal", 1, "GoAway")
			ETHP BCC 1 Bright A_Warp(AAPTR_TARGET, 32, 0, 28, 0, WARPF_INTERPOLATE)
		Loop
		GoAway:
			TNT1 A 1
		Stop
	}
}

Actor WandererShockwave_Real : DnD_ExplosiveBase {
	PROJECTILE
	RenderStyle Add
	Alpha 0.95
	Height 16
	Radius 8
	Stamina 4194304
	Speed 24
	Scale 1.1
	+THRUACTORS
	+DONTBLAST
	+FOILINVUL
	+FORCERADIUSDMG
	States {
		Spawn:
			TNT1 A 1
		Death:
			TNT1 A 1 A_Stop
			TNT1 A 0 A_SetUserVar(
				"user_expdmg", 
				DND_WEAPON_ASCENSION |
				(DND_DMGID_1 << CACHE_SHIFT) |
				(DND_DAMAGETYPE_OCCULT << DTYPE_SHIFT) |
				(200 << DPCT_SHIFT)
			)
			TNT1 A 0 A_SetUserVar("user_expradius", 160 * (1.0 + ACS_NamedExecuteWithResult("DND Explosion Radius Retrieve", DND_USETARGET) * exprad_factor))
			TNT1 A 0 A_SetUserVar("user_fullexpradius", 0)
			TNT1 A 0 A_SetUserVar("user_expflags", 0)
		Goto DoExplosionDamage
		ContinueFromExplosion:
			TNT1 A 0 A_SpawnItemEx ("DeathStaffFlare", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_NOCHECKPOSITION, 0)
			TNT1 A 0 A_CustomMissile("WandererShockwave_ExpansionDamager", 0, 0, 0, CMF_TRACKOWNER)
			TNT1 AAAAAA 0 A_SpawnItemEx("PurpleParticleSlowVanish",Random(-12,12),Random(-12,12),Random(-12,12),Random(-2,2),Random(-2,2),Random(-2,2),Random(-20,20),128)
			TNT1 A 0 A_PlaySound("DarkLich/VortexHit")
			TNT1 A 0 A_SpawnItemEx("WandererBlastShockwave", 0, 0, 16)
			TNT1 A 0 A_Quake(4, 24, 0, 320)
			TNT1 A 0 A_CustomMissile("WandererShockwave_MiniSpawner", 0, 0, 0, CMF_TRACKOWNER)
			ETHP DEFGHIJKLMNOP 3 Bright A_FadeOut(0.025)
			ETHP PPPPPP 1 Bright A_FadeOut(0.0875)
		Stop
	}
}

Actor WandererShockwave_ExpansionDamager : DnD_ExplosiveBase {
	PROJECTILE
	Speed 0
	Height 1
	Radius 1
	ReactionTime 7
	Stamina 4194304
	+THRUACTORS
	+FORCERADIUSDMG
	+FOILINVUL
	States {
		Spawn:
			TNT1 A 0
		XDeath:
		Death:
			TNT1 A 0 A_SetUserVar("user_expdmg", 
				DND_WEAPON_ASCENSION |
				(DND_DMGID_1 << CACHE_SHIFT) |
				(DND_DAMAGETYPE_OCCULT << DTYPE_SHIFT) |
				(85 << DPCT_SHIFT)
			)
			TNT1 A 0 A_SetUserVar("user_expradius", 160 * (1.0 + ACS_NamedExecuteWithResult("DND Explosion Radius Retrieve", DND_USETARGET) * exprad_factor))
			TNT1 A 0 A_SetUserVar("user_fullexpradius", user_expradius / 4)
			TNT1 A 0 A_SetUserVar("user_expflags", 0)
		Repeat:
			TNT1 AAAA 0 A_SpawnItemEx("PurpleParticleSlowVanish", frandom(-16 - (7 - reactiontime) * 3, 16 + (7 - reactiontime) * 3), frandom(-16 - (7 - reactiontime) * 3 , 16 + (7 - reactiontime) * 3), 0, frandom(-12.5, 12.5), frandom(-12.5, 12.5), frandom(-12.5, 12.5))
			TNT1 A 1
		Goto DoExplosionDamage
		ContinueFromExplosion:
			TNT1 A 0 A_SetUserVar("user_expradius", user_expradius * 1.275)
			TNT1 A 0 A_SetUserVar("user_fullexpradius", user_expradius / 4)
			TNT1 A 2 A_Countdown
			TNT1 A 0 A_JumpIf(reactiontime > 1, "Repeat")
			TNT1 A 1
		Stop
	}
}

Actor WandererBlastShockwave {
	Speed 0
	Scale 2.25 
	RenderStyle add
	Alpha 0.9
	+NOINTERACTION
	+CLIENTSIDEONLY
	States { 
		Spawn: 
			R076 ABCDEFFGGHHIIIJJJKKKLLMMNNOP 1 BRIGHT A_FadeOut(0.03)
		Stop 
	} 
} 

Actor WandererSmallExplosion : DnD_ExplosiveBase {
	Speed 0
	Height 1
	Radius 1
	Stamina 4194304
	Renderstyle Add
	Alpha 0.85
	+THRUACTORS
	+FORCERADIUSDMG
	+FOILINVUL
	States {
		Spawn:
			TNT1 A 0
		Death:
			TNT1 A 0 A_PlaySound("MageStaffExplode")
			BSBS EF 3 Bright
			TNT1 A 0 A_SetUserVar(
				"user_expdmg", 
				DND_WEAPON_ASCENSION |
				(DND_DMGID_1 << CACHE_SHIFT) |
				(DND_DAMAGETYPE_OCCULT << DTYPE_SHIFT) |
				(20 << DPCT_SHIFT)
			)
			TNT1 A 0 A_SetUserVar("user_expradius", 96 * (1.0 + ACS_NamedExecuteWithResult("DND Explosion Radius Retrieve", DND_USETARGET) * exprad_factor))
			TNT1 A 0 A_SetUserVar("user_fullexpradius", 0)
			TNT1 A 0 A_SetUserVar("user_expflags", 0)
		Goto DoExplosionDamage
		ContinueFromExplosion:
			BSBS GHIJKLM 3 Bright
		Stop
	}
}

Actor WandererShockwave_MiniSpawner {
	PROJECTILE
	Speed 0
	Height 1
	Radius 1
	const int max_amt = 15;
	const int r = 160;
	const int deg_base = 120;
	const int deg_area = deg_base / max_amt;
	var int user_count;
	+THRUACTORS
	States {
		Spawn:
		Death:
			TNT1 A 0
			TNT1 A 0 A_SetUserVar("user_count", 0)
		SpawnLoop:
			TNT1 A 0 A_JumpIf(user_count == max_amt, "Finish")
			TNT1 A 0 A_SpawnItemEx("WandererSmallExplosion", -r / 2 + r * cos(user_count * deg_area), -r * sin(user_count * deg_area), 16, 0, 0, 0, 0, SXF_TRANSFERPOINTERS)
			TNT1 A 0 A_SpawnItemEx("WandererSmallExplosion", r / 2  -r * cos(user_count * deg_area), -r * sin(user_count * deg_area), 16, 0, 0, 0, 0, SXF_TRANSFERPOINTERS)
			TNT1 A 0 A_SpawnItemEx("WandererSmallExplosion", -r / 2 + r * cos(user_count * deg_area), r * sin(user_count * deg_area), 16, 0, 0, 0, 0, SXF_TRANSFERPOINTERS)
			TNT1 A 3 A_SpawnItemEx("WandererSmallExplosion", r / 2 + -r * cos(user_count * deg_area), r * sin(user_count * deg_area), 16, 0, 0, 0, 0, SXF_TRANSFERPOINTERS)
			TNT1 A 0 A_SetUserVar("user_count", user_count + 1)
		Loop
		Finish:
			TNT1 A 1
		Stop
	}
}

Actor Wanderer_BlastCooldown : PowerProtection {
	damagefactor "normal", 1.0
	powerup.duration -5
}

Actor NoMoreAscension : PowerProtection {
	damagefactor "normal", 1.0
	powerup.duration 0x7FFFFFFF
}

Actor Cyborg_Perk1 : DnD_Boolean { }
Actor Cyborg_Perk20 : DnD_Boolean { }
Actor Cyborg_Perk40 : DnD_Boolean { }
Actor Cyborg_Perk60 : DnD_Boolean { }
Actor Cyborg_Perk80 : DnD_Boolean { }

Actor Cyborg_NoAnim : DnD_Boolean { }

Actor Cyborg_InstabilityStack : Inventory {
	inventory.maxamount 10
}

Actor Cyborg_Instability_Timer : Ammo {
	inventory.amount 1
	inventory.maxamount 165
	ammo.backpackamount 0
	ammo.backpackmaxamount 165
	+IGNORESKILL
}

Actor Cyborg_Instability_CD : PowerProtection {
	damagefactor "normal", 1.0
	powerup.duration 5
}

Actor Cyborg_Instability_StackGainCD : PowerProtection {
	damagefactor "normal", 1.0
	powerup.duration 3
}

Actor InstabilityBlast : DnD_BaseProjectile {
	Radius 6
	Height 12
	Speed 60
	Scale 0.75
	RenderStyle Add
	Alpha 1.0
	Decal "InstabilityDecal"
	DeathSound "Instability/Hit"
	
	Damage(
		ACS_NamedExecuteWithResult(
			"DND Player Damage Scale", 
			random(4, 32), 
			DND_DAMAGECATEGORY_ENERGY, 
			DND_USETARGET | DMG_ESHIELDSCALE,
			(20 << DEX_ATTUNE_BITS) | (30 << INT_ATTUNE_BITS)
		) | (DND_DAMAGETYPE_ENERGY << NONWEP_DMG_SHIFT)
	)
	
	Stamina 2097152
	
	+FOILINVUL
	+RANDOMIZE
	+DONTREFLECT
	States {
		SpawnState:
			TNT1 A 0 A_PlaySound("Instability/See", CHAN_5)
		SpawnStateLoop:
			CYP0 ABCDEFGHIJ 1 Bright
		Loop
		Death:
			TNT1 A 0 A_SpawnItemEx ("PlasmaFlare", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_NOCHECKPOSITION, 0)
			CYP0 KLMNOPQRSTUVW 1 Bright
		Stop
	}
}

Actor Berserker_Perk1 : DnD_Boolean { }
Actor Berserker_Perk20 : DnD_Boolean { }
Actor Berserker_Perk40 : DnD_Boolean { }
Actor Berserker_Perk60 : DnD_Boolean { }
Actor Berserker_Perk80 : DnD_Boolean { }

Actor Berserker_DamageTracker : DnD_Info { }
Actor Berserker_DamageTimer : Inventory {
	inventory.maxamount 122 // 17.5 x 7
}
Actor Berserker_Perk20_CD : PowerDamage {
	damagefactor "normal", 1.0
	powerup.duration 10
}


Actor Berserker_HitTracker : DnD_Info { }
Actor Berserker_Perk60_HitCounter : DnD_Info { }
Actor Berserker_Perk60_HitCooldown : PowerProtection {
	damagefactor "normal", 1.0
	powerup.duration 5
}
Actor Berserker_HitTimer : Inventory {
	inventory.maxamount 70
}

Actor Berserker_Perk60_Speed : PowerSpeed {
	powerup.duration 0x7FFFFFFF
	Speed 1.3
}

Actor Berserker_NoRoar : DnD_Boolean { }

Actor Berserker_ThunderSpark : WhiteMagicSpark {
	Scale 0.125
}

Actor Berserker_Perk80_Extension : PowerProtection {
	damagefactor "normal", 1.0
	powerup.duration -30
}

Actor Berserker_ThunderFX {
	+NOINTERACTION
	+CLIENTSIDEONLY
	translation "80:111=192:207"
	Renderstyle Add
	Alpha 0.2
	Scale 1.25
	states {
		Spawn:
			TNT1 A 0 NoDelay A_PlaySound("Berserker/Thunder")
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Berserker_ThunderSpark", 0, 0, 0, random(1,10), 0, random(1,10), random(0,359), 129)
			THND AAAA 1 Bright A_FadeIn(0.2)
			THND AAAA 2 Bright A_FadeOut(0.2)
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1  Bright A_SpawnItemEx("RocketSmoke", 0, 0, 0, random(0,3), 0, random(2,3), random(0,359), 128)
		Stop
	}     	
}

Actor Berserker_ThunderFX_Spawner : DnD_Activator {
	States {
		Pickup:
			TNT1 A 0 A_SpawnItemEx("Berserker_ThunderFX")
		Stop
	}
}

Actor NoMoreGoW : PowerProtection {
	damagefactor "normal", 1.0
	powerup.duration 0x7FFFFFFF
}

Actor Trickster_Perk1 : DnD_Boolean { }
Actor Trickster_Perk20 : DnD_Boolean { }
Actor Trickster_Perk40 : DnD_Boolean { }
Actor Trickster_Perk60 : DnD_Boolean { }

Actor Trickster_Acrobat_Cooldown : PowerProtection {
	damagefactor "normal", 1.0
	powerup.duration -4
}

Actor Trickster_Perk80 : DnD_Boolean { }
Actor NoMoreDoppelganger : PowerProtection {
	damagefactor "normal", 1.0
	powerup.duration 0x7FFFFFFF
}

Actor Trickster_ShadowCooldown : DnD_Info {
	inventory.maxamount 450 // 90 second cooldown, reduces every 7 tics
}

Actor TricksterShadowClone {
	Species "Player"
	Health 0x7FFFFFFF
	DamageType "NoRealDamage"
	Renderstyle Translucent
	+FRIENDLY
	+SHOOTABLE
	+SOLID
	+THRUSPECIES
	+QUICKTORETALIATE
	+NOPAIN
	Radius 16
	Height 56
	Scale 0.63
	Alpha 0.025
	States {
		Spawn:
			TNT1 A 0 NoDelay A_SpawnItemEx("DissipationEffect")
			TNT1 A 5 A_PlaySound("Trickster/Split")
			ELEE A 1 A_GiveInventory("TricksterMover", 1)
			ELEE AAA 1 A_FadeIn(0.05)
			ELEE B 1 A_GiveInventory("TricksterMover", 1)
			ELEE BBB 1 A_FadeIn(0.0625)
			TNT1 A 0 A_Explode(1, 512, XF_NOTMISSILE, 0, 512)
			ELEE C 1 A_GiveInventory("TricksterMover", 1)
			ELEE CCC 1 A_FadeIn(0.075)
			ELEE D 1 A_GiveInventory("TricksterMover", 1)
			ELEE DDD 1 A_FadeIn(0.09)
			TNT1 A 0 A_Explode(1, 512, XF_NOTMISSILE, 0, 512)
			ELEE E 1 A_GiveInventory("TricksterMover", 1)
			ELEE EEE 1 A_FadeIn(0.1)
			ELEE F 4 A_GiveInventory("TricksterMover", 1)
			ELEE FFF 1 A_FadeIn(0.1)
			TNT1 A 0 A_Explode(1, 512, XF_NOTMISSILE, 0, 512)
			TNT1 A 0 ACS_NamedExecuteWithResult("DnD Timed Monster", 15)
		Walk:
			ELEE ABCDEF 4 A_GiveInventory("TricksterMover", 1)
			TNT1 A 0 A_Jump(32, "Attack")
			ELEE ABCDEF 4 A_GiveInventory("TricksterMover", 1)
			TNT1 A 0 A_Jump(64, "Attack")
			ELEE ABCDEF 4 A_GiveInventory("TricksterMover", 1)
			TNT1 A 0 A_Jump(96, "Attack")
			ELEE ABCDEF 4 A_GiveInventory("TricksterMover", 1)
			TNT1 A 0 A_Jump(128, "Attack")
		Loop
		Attack:
			// Emit taunters
			TNT1 A 0 A_Explode(1, 512, XF_NOTMISSILE, 0, 512)
			ELEE JKL 4
		Goto Walk
		Remove:
			ELEE S 5 A_PlaySound("Trickster/Fade")
			TNT1 A 0 A_SpawnItemEx("DissipationEffect")
			TNT1 A 0 A_Stop
			ELEE S 1 A_FadeOut(0.0875)
		Wait
	}
}

Actor TricksterShadowClone_Random : TricksterShadowClone {
	States {
		Spawn:
			TNT1 A 0 NoDelay A_SpawnItemEx("DissipationEffect")
			TNT1 A 5 A_PlaySound("Trickster/Split")
			ELEE A 1 A_GiveInventory("TricksterMover_Random", 1)
			ELEE AAA 1 A_FadeIn(0.05)
			ELEE B 1 A_GiveInventory("TricksterMover_Random", 1)
			ELEE BBB 1 A_FadeIn(0.0625)
			TNT1 A 0 A_Explode(1, 512, XF_NOTMISSILE, 0, 512)
			ELEE C 1 A_GiveInventory("TricksterMover_Random", 1)
			ELEE CCC 1 A_FadeIn(0.075)
			ELEE D 1 A_GiveInventory("TricksterMover_Random", 1)
			ELEE DDD 1 A_FadeIn(0.09)
			TNT1 A 0 A_Explode(1, 512, XF_NOTMISSILE, 0, 512)
			ELEE E 1 A_GiveInventory("TricksterMover_Random", 1)
			ELEE EEE 1 A_FadeIn(0.1)
			ELEE F 4 A_GiveInventory("TricksterMover_Random", 1)
			ELEE FFF 1 A_FadeIn(0.1)
			TNT1 A 0 A_Explode(1, 512, XF_NOTMISSILE, 0, 512)
			TNT1 A 0 ACS_NamedExecuteWithResult("DnD Timed Monster", 15)
		Walk:
			ELEE ABCDEF 4 A_GiveInventory("TricksterMover_Random", 1)
			TNT1 A 0 A_Jump(32, "Attack")
			ELEE ABCDEF 4 A_GiveInventory("TricksterMover_Random", 1)
			TNT1 A 0 A_Jump(64, "Attack")
			ELEE ABCDEF 4 A_GiveInventory("TricksterMover_Random", 1)
			TNT1 A 0 A_Jump(96, "Attack")
			ELEE ABCDEF 4 A_GiveInventory("TricksterMover_Random", 1)
			TNT1 A 0 A_Jump(128, "Attack")
		Loop
	}
}

Actor TricksterMover : DnD_Activator {
	States {
		Pickup:
			TNT1 A 0 A_Jump(128, "RandomAngle")
			TNT1 A 0 A_FaceTarget
		Goto Move
		RandomAngle:
			TNT1 A 0 A_SetAngle(frandom(0, 360))
		Move:
			TNT1 A 0 A_ChangeVelocity(8 * cos(angle), 8 * sin(angle), velz, CVF_REPLACE)
			TNT1 A 0 A_Jump(8, "Jumper")
		Stop
		Jumper:
			TNT1 A 0 ThrustThingZ(0, random(32, 48), 0, 1)
		Stop
	}
}

Actor TricksterMover_Random : DnD_Activator {
	States {
		Pickup:
			TNT1 A 0 A_SetAngle(frandom(0, 360))
			TNT1 A 0 A_ChangeVelocity(8 * cos(angle), 8 * sin(angle), velz, CVF_REPLACE)
			TNT1 A 0 A_Jump(8, "Jumper")
		Stop
		Jumper:
			TNT1 A 0 ThrustThingZ(0, random(32, 48), 0, 1)
		Stop
	}
}

Actor Trickster_Pointer {
	Damage 0
	Scale 0.4
	+NOGRAVITY
	+PUFFONACTORS
	+ALWAYSPUFF
	+PUFFGETSOWNER
	States {
		Spawn:
			TNT1 A 0 NoDelay ACS_NamedExecuteWithResult("DnD Trickster Pointer")
			TNT1 A 1 A_Recoil(-8)
			TNT1 A 1 A_Stop
			TRCK Z 350
		Stop
	}
}

Actor TricksterShadowClone_Targeted : TricksterShadowClone {
	Monster
	Speed 8
	-COUNTKILL
	-NODROPOFF
	+NOBLOCKMONST
	-CANUSEWALLS
	+FLOORCLIP
	
	// a tiny bit bigger than the player to ensure the player does not get stuck teleporting around
	Height 64
	Radius 18
	
	MaxDropOffHeight 128
	MaxStepHeight 32
	
	const int reach_dist = 108;
	
	States {
		Spawn:
			TNT1 A 0 NoDelay A_SpawnItemEx("DissipationEffect")
			TNT1 A 5 A_PlaySound("Trickster/Split")
			TNT1 A 0 ACS_NamedExecuteWithResult("DnD Fade Effect", 5, 10, 24, 12)
			ELEE AAAA 1 A_Chase("", "")
			ELEE BBBB 1 A_Chase("", "")
			TNT1 A 0 A_Explode(1, 512, XF_NOTMISSILE, 0, 512)
			ELEE CCCC 1 A_Chase("", "")
			ELEE DDDD 1 A_Chase("", "")
			TNT1 A 0 A_Explode(1, 512, XF_NOTMISSILE, 0, 512)
			ELEE EEEE 1 A_Chase("", "")
			ELEE FFFF 1 A_Chase("", "")
			TNT1 A 0 A_Explode(1, 512, XF_NOTMISSILE, 0, 512)
			TNT1 A 0 ACS_NamedExecuteWithResult("DnD Timed Monster", 20)
		Walk:
			ELEE AAAABBBBCCCC 1 A_Chase("", "")
			TNT1 A 0 A_JumpIfCloser(reach_dist, "WalkAtDestination")
			ELEE DDDDEEEEFFFF 1 A_Chase("", "")
			TNT1 A 0 A_Jump(32, "Attack_Chase")
			ELEE AAAABBBBCCCC 1 A_Chase("", "")
			TNT1 A 0 A_JumpIfCloser(reach_dist, "WalkAtDestination")
			ELEE DDDDEEEEFFFF 1 A_Chase("", "")
			TNT1 A 0 A_Jump(64, "Attack_Chase")
			ELEE AAAABBBBCCCC 1 A_Chase("", "")
			TNT1 A 0 A_JumpIfCloser(reach_dist, "WalkAtDestination")
			ELEE DDDDEEEEFFFF 1 A_Chase("", "")
			TNT1 A 0 A_Jump(96, "Attack_Chase")
			ELEE AAAABBBBCCCC 1 A_Chase("", "")
			TNT1 A 0 A_JumpIfCloser(reach_dist, "WalkAtDestination")
			ELEE DDDDEEEEFFFF 1 A_Chase("", "")
			TNT1 A 0 A_Jump(128, "Attack_Chase")
		Loop
		WalkAtDestination:
			TNT1 A 0 ACS_NamedExecuteAlways("DnD Trickster Swap Checker", 0)
		WalkAtDestinationLoop:
			ELEE ABCDEF 4 A_GiveInventory("TricksterMover_Random", 1)
			TNT1 A 0 A_Jump(32, "Attack")
			ELEE ABCDEF 4 A_GiveInventory("TricksterMover_Random", 1)
			TNT1 A 0 A_Jump(64, "Attack")
			ELEE ABCDEF 4 A_GiveInventory("TricksterMover_Random", 1)
			TNT1 A 0 A_Jump(96, "Attack")
			ELEE ABCDEF 4 A_GiveInventory("TricksterMover_Random", 1)
			TNT1 A 0 A_Jump(128, "Attack")
		Loop
		Attack_Chase:
			// Emit taunters
			TNT1 A 0 A_Explode(1, 512, XF_NOTMISSILE, 0, 512)
			ELEE JKL 4
		Goto Walk
		Attack:
			// Emit taunters
			TNT1 A 0 A_Explode(1, 512, XF_NOTMISSILE, 0, 512)
			ELEE JKL 4
		Goto WalkAtDestinationLoop
	}
}

Actor Trickster_Swap : DnD_Activator {
	States {
		Pickup:
			TNT1 A 0 A_SetBlend("Black", 0.7, 30)
			TNT1 A 0 A_SpawnItemEx("DissipationEffect")
			TNT1 A 0 A_PlaySound("Trickster/Swap",CHAN_AUTO,1.0,false,1.5)
		Stop
	} 
}

Actor Trickster_SwapCount : Inventory {
	inventory.maxamount 2
}

Actor TricksterSwapSignal : PowerProtection {
	damagefactor "normal", 1.0
	powerup.duration 3
}

Actor TricksterSwapCooldown : PowerProtection {
	damagefactor "normal", 1.0
	powerup.duration 52
}