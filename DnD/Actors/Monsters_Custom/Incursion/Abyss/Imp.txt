Actor AbyssalImp : DnD_BaseHybridMonster { 
	Health 0x7FFFFFFF
	Species "Imp"
	Radius 20
	Height 56
	Mass 100
	Speed 10
	PainChance 160
	GibHealth -10
	seesound "monster/dvlsit"
	painsound "monster/dvlpai"
	deathsound "monster/dvldth"
	activesound "monster/dvlact"
	Obituary "%o was charred by an Abyssal Imp."
	Tag "Abyssal Imp"
	Scale 0.8
	+ISMONSTER
	States {
		SetInfo:
			TNT1 A 0 A_SetArg(4, DND_ABYSS_IMP)
		Goto SpawnSetup

		IdleState:
			C017 AB 10 Fast A_GiveInventory("DnD_SightChecker", 1)
		Loop
		SeeState:
			C017 AABBCCDD 3 Fast A_Chase
		Goto See
		MissileState:
			C017 IJ 7 Fast A_FaceTarget
			C017 K 6 Fast A_CustomMissile("AbyssImpBall", 32)
		Goto See
		MeleeState:
			C017 EF 7 Fast A_FaceTarget
			C017 G 6 Fast A_CustomMeleeAttack(3 * random(5, 9), "imp/melee", "")
		Goto See
		PainState:
			C017 H 3 Fast
			C017 H 3 Fast A_Pain
		Goto See
		DeathState:
			C017 L 8
			C017 M 8 A_Scream
			C017 N 6
			C017 O 6 A_NoBlocking
			C017 P -1
		Stop
		XDeathState:
			TNT1 A 0 A_SpawnDebris("NashGore_Gib9",0)
			TNT1 A 0 A_SpawnDebris("NashGore_Gib10",0)
			TNT1 A 0 A_SpawnDebris("NashGore_Gib11",0)
			TNT1 A 0 A_SpawnDebris("NashGore_Gib11",0)
			TNT1 A 0 A_SpawnItemEx("GenericGibber_Low", 0, 0, 0, 0, 0, 0, 0, SXF_USEBLOODCOLOR)
			C017 Q 5
			C017 R 5 A_XScream
			C017 S 5
			C017 T 5 A_NoBlocking
			C017 UVW 5
			C017 X -1
		Stop
		RaiseState:
			C017 ONML 8
		Goto See
	}
}

Actor AbyssImpBall {
	PROJECTILE
	Speed 25
	Height 10
	Radius 5
	Damage (3 * random(6, 9))
	DamageType "Fire"
	Stamina 1073741856 // fire
	+DONTBLAST
	SeeSound "abyssal_demon/fireball"
	DeathSound "abyssal_demon/fireballhit"
	Decal "DoomImpScorch"
	States {
		Spawn:	
			TNT1 A 0 NoDelay ACS_NamedExecuteAlways("DnD Pierce Check")
		SpawnLoop:
			CP04 ABC 3 Bright
        Loop
		Death:
			TNT1 A 0
			TNT1 A 0 A_SpawnItemEx("RedParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128)
			TNT1 AAA 0 A_SpawnItemEx("ExplosionFX2", 0, 0, 0, frandom(-1, 1), frandom(-1, 1), frandom(-1, 1))
			TNT1 AAA 0 A_CustomMissile("FireballSmoke", 0, 0, random(0,360), 2, random(0,360))
			CP04 DEFGHI 3 Bright
        Stop
	}
}

ACTOR AbyssalImp_Elite : AbyssalImp { 
	States {
		SetInfo:
			TNT1 A 0 A_SetArg(3, DND_MONSTERIS_ELITE)
			TNT1 A 0 A_SetArg(4, DND_ABYSS_IMP)
		Goto SpawnSetup
	}
}