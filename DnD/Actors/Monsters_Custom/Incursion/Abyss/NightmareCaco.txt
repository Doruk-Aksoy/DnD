Actor AbyssalNightmareCaco : DnD_BaseHybridMonster {
	Health 0x7FFFFFFF
	Species "Cacodemon"
	Tag "Abyssal Nightmare Cacodemon"
	Radius 31
	Height 56
	Mass 1024
	Speed 9
	PainChance 32
	+FLOAT
	+NOGRAVITY
	+DONTHURTSPECIES
	+DONTRIP
	+MISSILEMORE
	+MISSILEEVENMORE
	-FLOORCLIP
	SeeSound "caco/sight"
	Painsound "Cacodemon/Pain"
	DeathSound "caco/death"
	ActiveSound "caco/active"
	Obituary "$OB_CACO"
	HitObituary "$OB_CACOHIT"
	Accuracy 50
	XScale 0.6
	YScale 0.65
	+ISMONSTER
	States {
		SetInfo:
			TNT1 A 0 A_SetArg(4, DND_ABYSS_NIGHTMARECACO)
		Goto SpawnSetup
	 
		IdleState:
			C023 A 10 A_GiveInventory("DnD_SightChecker", 1)
		Loop
		SeeState:
			C023 A 0 A_CheckSight("NormalChase")
			C023 AABB 3 Fast A_FastChase
			CCDD CCDD 3 Fast A_FastChase
		Goto See
		NormalChase:
			C023 AABBCCDD 3 Fast A_Chase
		Goto See
		MissileState:
		MeleeState:
			C023 EF 4 Fast Bright A_FaceTarget
			TNT1 A 0 A_Recoil(-1.6)
			C023 G 5 Fast bright A_CustomComboAttack("AbyssNightmareCacoBall", 32, 10 * random(6, 7), "", "")
		goto See
		PainState:
			C023 D 3 Fast
			C023 D 3 Fast A_Pain
		Goto See
		XDeathState:
			TNT1 A 0 A_SpawnItemEx("GenericGibber_Medium", 0, 0, 0, 0, 0, 0, 0, SXF_USEBLOODCOLOR)
		DeathState:
			C023 H 8
			C023 I 8 A_Scream
			C023 JK 8
			TNT1 A 0 A_SpawnItemEx("GenericGibber_Low", 0, 0, 0, 0, 0, 0, 0, SXF_USEBLOODCOLOR)
			C023 LM 8 A_NoBlocking
			C023 N -1 A_SetFloorClip
		Stop
		RaiseState:
			C023 M 8 A_UnSetFloorClip
			C023 LKJIH 8
		Goto See
	}
}

Actor AbyssNightmareCacoBall : Cacodemonball {
	Damage (6 * random(6, 8))
	Speed 32
	+DONTBLAST
	DamageType "MagicalBlue"
	Stamina 1073742080
	States {
		Spawn:
			TNT1 A 0 NoDelay ACS_NamedExecuteAlways("DND Pierce Check")
		SpawnLoop:
			CP07 ABC 3 Bright
		Loop
		Death:
			TNT1 A 0 A_SpawnItemEx("BlueParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128)
			TNT1 AAA 0 A_SpawnItemEx("ExplosionFX5", 0, 0, 0, frandom(-1, 1), frandom(-1, 1), frandom(-1, 1))
			TNT1 AAA 0 A_CustomMissile("FireballSmoke", 0, 0, random(0,360), 2, random(0,360))
			CP07 DEFGHIJ 3 Bright
		Stop
	}
}