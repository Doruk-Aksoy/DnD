ACTOR FlemoidBernabe : DnD_BaseHybridMonster {
	Health 0x7FFFFFFF
	Mass 1500
	Speed 12
	Height 56
	Radius 20
	ProjectilePassHeight 72
	Painchance 2
	Bloodcolor "72 24 24"
	Seesound "qbaron/sight"
	Painsound "qbaron/pain"
	Deathsound "qbaron/death"
	Activesound "qbaron/active"
	Translation "112:127=%[0.00,0.00,0.00]:[1.23,0.90,0.39]"
	Minmissilechance 32
	Species "Chex"
	XScale 0.75
	YScale 0.8
	Tag "Flemoid Bernabe"
	+DONTHURTSPECIES
	+DONTHARMSPECIES
	+ISMONSTER
	+FLOORCLIP
	+QUICKTORETALIATE
	-DROPOFF
	+DONTRIP
	+NORADIUSDMG
	Obituary "%o was defeated by the Bernabe (He's dangerous)."
	states {		
		SetInfo:
			TNT1 A 0 A_SetArg(4, DND_CHEX_BERNABE)
			TNT1 A 0 ACS_NamedExecuteWithResult("DnD Init Monster Data", DND_MWEIGHT_VERYRARE)
		Goto SpawnSetup

		SeeState:
			C009 A 3 A_Chase
			C009 A 0 A_PlaySound("gibbage/bslide")
			C009 A 3 A_Chase
			C009 BBC 3 A_Chase
			C009 C 0 A_PlaySound("gibbage/bslide")
			C009 C 3 A_Chase
			C009 DD 3 A_Chase
		loop
		IdleState:
			C009 AABB 3 A_GiveInventory("DnD_SightChecker", 1)
		loop
		MeleeState:
		MissileState:
			C009 E 0 A_Jump(96, "Spammy")
			C009 EF 6 A_FaceTarget
			C009 G 7 A_CustomMissile ("BernabeBall", 52, 0, 0)
			C009 FF 3 A_FaceTarget
			C009 G 7 A_CustomMissile ("BernabeBall", 52, 0, 0)
			C009 FF 3 A_FaceTarget
			C009 G 7 A_CustomMissile ("BernabeBall", 52, 0, 0)
		goto See
		Spammy:
			C009 EF 6 A_FaceTarget
			C009 GGG 2 A_CustomMissile ("BernabeBall2", 52, 0, frandom(-5.25, 5.25))
			C009 G 1 A_CustomMissile ("BernabeBall", 52, 0, 0)
			C009 FF 3 A_FaceTarget
			C009 GGG 2 A_CustomMissile ("BernabeBall2", 52, 0, frandom(-5.25, 5.25))
			C009 G 1 A_CustomMissile ("BernabeBall", 52, 0, 0)
			C009 FF 3 A_FaceTarget
			C009 GGG 2 A_CustomMissile ("BernabeBall2", 52, 0, frandom(-5.25, 5.25))
			C009 G 1 A_CustomMissile ("BernabeBall", 52, 0, 0)
			C009 FF 3 A_FaceTarget
			C009 GGGGGGGGGG 2 A_CustomMissile ("BernabeBall2", 52, 0, frandom(-9.25, 9.25))
		Goto See
		PainState:
			C009 H 2
			C009 H 2 A_Pain
		goto See
		XDeathState:
		DeathState:
			C009 I 1 A_Scream
			C009 I 3 A_SpawnItemEx("GenericGibber_Medium_Delayed", 0, 0, 0, 0, 0, 0, 0, SXF_USEBLOODCOLOR)
			C009 JKL 4
			TNT1 A 0 A_NoBlocking
			C009 MN 5
			C009 O 5 A_KillMaster
		stop
		Death.MagicSealing:
			"####" "#" 0 A_KillMaster
		Goto Super::Death.MagicSealing
		Death.Emerald:
			"####" "#" 0 A_KillMaster
		Goto Super::Death.Emerald
		Death.Ice:
		Death.P_Ice:
			"####" "#" 0 A_KillMaster
		Goto Super::Death.Ice
	}
}

Actor BernabeBall : MaximusBall {
	Translation "112:127=%[0.00,0.00,0.00]:[1.23,0.90,0.39]"
}

Actor BernabeBall2 : QuadrumpusBall {
	Translation "112:127=%[0.00,0.00,0.00]:[1.23,0.90,0.39]"
}

ACTOR FlemoidBernabe_Elite : FlemoidBernabe { 
	States {
		SetInfo:
			TNT1 A 0 A_SetArg(3, DND_MONSTERIS_ELITE)
			TNT1 A 0 A_SetArg(4, DND_CHEX_BERNABE)
			TNT1 A 0 ACS_NamedExecuteWithResult("DnD Init Monster Data", DND_MWEIGHT_VERYRARE)
		Goto SpawnSetup
	}
}