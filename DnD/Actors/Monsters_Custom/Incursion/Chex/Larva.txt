ACTOR FlemoidLarva : DnD_BaseMeleeMonster {
	Health 0x7FFFFFFF
	Mass 225
	Speed 17
	Height 46
	Radius 24
	Meleerange 84
	Painchance 40
	Bloodcolor "Green"
	Seesound "qspectre/sight"
	Attacksound "qspectre/melee"
	Painsound "qspectre/pain"
	Deathsound "qspectre/death"
	Activesound "qspectre/active"
	Species "Chex"
	Tag "Flemoid Larva"
	Stamina 128
	+ISMONSTER
	+PUSHABLE
	+QUICKTORETALIATE
	+FLOORCLIP
	+DONTHURTSPECIES
	+DONTHARMSPECIES
	-COUNTKILL
	-DROPOFF
	Hitobituary "%o was gooed by a Flemoid Larva."
	states {
		SetInfo:
			TNT1 A 0 A_SetArg(4, DND_CHEX_LARVA)
			TNT1 A 0 ACS_NamedExecuteWithResult("DnD Init Monster Data", DND_MWEIGHT_RARE1)
		Goto SpawnSetup
		
		SeeState:
			C003 ABBCCD 2 A_Chase
		loop
		IdleState:
			C003 A 0 A_LookEx(0, 0, 0, 0, 360, "See")
			C003 AABBCCDD 2 A_Wander
			C003 A 0 A_LookEx(0, 0, 0, 0, 360, "See")
		loop
		MeleeState:
			C003 EF 7 A_FaceTarget
			C003 G 8 A_CustomMeleeAttack (random(18, 25), "qdemon/melee")
		goto See
		PainState:
			C003 H 2
			C003 H 2 A_Pain
		goto See
		DeathState:
			C003 I 8
			C003 J 8 A_Scream
			C003 K 4
			C003 L 4 A_NoBlocking
			C003 M 4
			CHLV A -1
		stop
		XDeathState:    
			C003 I 1 A_SpawnitemEx ("NashGore_Gib1", 0, 0, random(0,42), 0, 0, random(2,6), random(0,360), SXF_USEBLOODCOLOR)
			C003 I 1 A_SpawnitemEx ("NashGore_Gib2", 0, 0, random(0,42), 0, 0, random(2,6), random(0,360), SXF_USEBLOODCOLOR)
			C003 I 1 A_SpawnitemEx ("NashGore_Gib3", 0, 0, random(0,42), 0, 0, random(2,6), random(0,360), SXF_USEBLOODCOLOR)
			C003 I 4 A_SpawnItemEx("GenericGibber_Low_Delayed", 0, 0, 0, 0, 0, 0, 0, SXF_USEBLOODCOLOR)
			C003 J 8 A_Scream
			C003 K 4 
			C003 L 4 A_NoBlocking
			C003 M 4
			C003 N 5
		stop
	}
}