// Sprites by Eriance
Actor "Rocket Launcher" : RocketLauncher
{	
	Weapon.Slotnumber 5
	Inventory.PickupSound "weapons/pickup"
	+INVENTORY.UNDROPPABLE
    Weapon.BobStyle InverseSmooth
    Weapon.BobSpeed 1.6
    Weapon.BobRangeY 0.5
    Weapon.BobRangeX 0.9
	Weapon.AmmoGive 0
	Tag "$WEP_47_TAG"
   States
   {
   Spawn:
      LAUN A -1
      Stop
   Ready:
      MIS9 A 1 A_WeaponReady
      Loop
   Deselect:
	  TNT1 A 0 A_TakeInventory("H_WeaponSlot5", 1)
      MIS9 A 1 A_Lower
	  TNT1 A 0 A_Lower
      Loop
   Select:
	  TNT1 A 0 A_GiveInventory("H_WeaponSlot5", 1)
	  TNT1 A 0 A_TakeInventory("DnD_WeaponID", 0x7FFFFFFF)
	  TNT1 A 0 A_GiveInventory("DnD_WeaponID", DND_WEAPON_ROCKETLAUNCHER)
	  TNT1 A 0 ACS_NamedExecuteWithResult("DND Weapon Damage Cache", DND_DMGID_0, 45, 2 | (4 << 16), DND_WEAPON_ROCKETLAUNCHER)
	  TNT1 A 0 ACS_NamedExecuteWithResult("DND Weapon Damage Cache", DND_DMGID_1, 128, 0, DND_WEAPON_ROCKETLAUNCHER)
  SelectLoop:
      MIS9 A 1 A_Raise
	  TNT1 A 0 A_Raise
      Loop
   Fire:
      MIS9 D 3 Bright A_GunFlash
	  TNT1 A 0 Radius_Quake(4,3,0,1,0)
      MIS9 E 4 Bright A_GiveInventory("RocketShooter", 1)
      MIS9 FGHC 3 Bright
	  MIS9 B 4 Bright
      MIS9 A 0 A_Refire
      Goto Ready
   Flash:
      TNT1 A 3 A_Light1
      TNT1 A 4 A_Light2
      TNT1 A 3 A_Light1
      TNT1 A 2 A_Light0
      stop
   }
}

Actor RocketShooter : DnD_Activator {
	States {
		Use:
			TNT1 A 0 ACS_NamedExecuteAlways("DnD Ammo Gain Chance", 0, DND_AMMOSLOT_ROCKET, AMMO_ROCKET, 1)
			TNT1 A 0 A_GiveInventory("DnD_UsedNonSpecial", 1)
			TNT1 A 0 A_FireCustomMissile("RocketX", 0, 1, 0, 0)
		Stop
	}
}

Actor "Upgraded Rocket Launcher" : RocketLauncher {	
	Weapon.Slotnumber 5
	Inventory.PickupSound "weapons/pickup"
	+INVENTORY.UNDROPPABLE
	Tag "$WEP_48_TAG"
    Weapon.BobStyle InverseSmooth
    Weapon.BobSpeed 1.6
    Weapon.BobRangeY 0.5
    Weapon.BobRangeX 0.9
	-NOAUTOFIRE
	States {
		Spawn:
			WPPK G -1
		Stop
		Ready:
			RLDI A 1 A_WeaponReady
		Loop
		Deselect:
			TNT1 A 0 A_TakeInventory("H_WeaponSlot5", 1)
			RLDI A 1 A_Lower
			TNT1 A 0 A_Lower
		Loop
		Select:
			TNT1 A 0 A_GiveInventory("H_WeaponSlot5", 1)
			TNT1 A 0 A_TakeInventory("DnD_WeaponID", 0x7FFFFFFF)
			TNT1 A 0 A_GiveInventory("DnD_WeaponID", DND_WEAPON_TORPEDOLAUNCHER)
			TNT1 A 0 ACS_NamedExecuteAlways("DnD Weapon Input Handler", 0, DND_WEAPON_TORPEDOLAUNCHER)
			TNT1 A 0 ACS_NamedExecuteWithResult("DND Weapon Damage Cache", DND_DMGID_0, 20, 15 | (20 << 16), DND_WEAPON_TORPEDOLAUNCHER)
			TNT1 A 0 ACS_NamedExecuteWithResult("DND Weapon Damage Cache", DND_DMGID_1, 224, 0, DND_WEAPON_TORPEDOLAUNCHER)
	    SelectLoop:
			RLDI A 1 A_Raise
			TNT1 A 0 A_Raise
		Loop
		Fire:
			RLDI A 1
			RLDF A 1 Bright A_GunFlash
			TNT1 A 0 A_PlayWeaponSound("weapons/newrocketfire")
			TNT1 A 0 Radius_Quake(4, 3, 0, 1,0)
			RLDF B 1 Bright A_GiveInventory("RocketShooter2", 1)
			RLDF C 1 Bright Offset(14, 39)
			RLDF D 1 Bright Offset(19, 42)
			RLDF E 1 Bright Offset(21, 44)
			RLDF F 2 Offset(23, 45)
			RLDR A 1 Offset(21, 44)
			RLDR A 1 Offset(19, 43)
			RLDR B 1 Offset(17, 42)
			RLDR B 1 Offset(15, 41)
			RLDR C 1 Offset(13, 40)
			RLDR C 1 Offset(11, 39)
			RLDR D 1 Offset(9, 38)
			RLDR D 1 Offset(7, 37)
			RLDR E 1 Offset(5, 36)
			RLDR E 1 Offset(3, 35)
			RLDR F 1 Offset(1, 33)
			RLDR F 1 Offset(0, 32)
			RLDR G 1 Offset(0, 32)
			RLDR H 1 Offset(0, 32)
			RLDR I 1 Offset(0, 32)
			RLDR JKLMNOPQRST 1 Offset(0, 32)
			RLDI A 4
		Goto Ready
		Flash:
			TNT1 A 3 A_Light1
			TNT1 A 4 A_Light2
			TNT1 A 3 A_Light1
			TNT1 A 2 A_Light0
		stop
	}
}

Actor TorpedoBoomSignal : PowerDamage {
	damagefactor "normal", 1.0
	powerup.duration 3
}

Actor TorpedoTimer : PowerDamage {
	damagefactor "normal", 1.0
	powerup.duration 10
}

Actor RocketX : DnD_ExplosiveBase {
	SeeSound "weapons/rocklf"
	DeathSound "weapons/rocklx"
	Radius 11
	Height 8
	Speed 20
	Decal "Scorch"
	+DONTBLAST
	+THRUGHOST
	States {
		SpawnState:
			MISL A 2 Bright A_SpawnItemEx("DnD_RocketTrail", -12, 0, 5)
		Loop
		XDeath:
			TNT1 A 0 ACS_NamedExecuteWithResult("DnD Do Impact Damage", ACS_NamedExecuteWithResult("DND Weapon Damage Retrieve", DND_DMGID_0 | (DND_WEAPON_ROCKETLAUNCHER << 16), TALENT_EXPLOSIVE, DND_USETARGET | DND_ISSLOT5), DND_DAMAGETYPE_EXPLOSIVES, 0, DND_WEAPON_ROCKETLAUNCHER)
		Death:
			TNT1 A 0 A_SpawnItemEx ("ExplosionFlare", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_NOCHECKPOSITION, 0)
			TNT1 AAAAA 0 A_CustomMissile("RocketSmoke", 0, 0, random(0, 360), 2, random(0, 180))
			TNT1 AAAAAAAA 0 A_CustomMissile("RocketSmoke", 0, 0, random(0, 360), 2, random(0, 360))
			TNT1 AAAAAAAAA 0 A_CustomMissile ("ExplosionParticleHeavy", 0, 0, random (0, 360), 2, random (0, 180))
			TNT1 AAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("ExplosionParticleHeavy", 0, 0, random (0, 360), 2, random (0, 360))
			TNT1 AAAAAAAAA 0 A_CustomMissile ("ExplosionParticleVeryFast", 0, 0, random (0, 360), 2, random (0, 360))
			
			TNT1 A 0 A_SetUserVar("user_expdmg", ACS_NamedExecuteWithResult("DND Weapon Damage Retrieve", DND_DMGID_1 | (DND_WEAPON_ROCKETLAUNCHER << 16), TALENT_EXPLOSIVE, DND_USETARGET | DND_ISSLOT5))
			TNT1 A 0 A_SetUserVar("user_expradius", 144 * (1.0 + ACS_NamedExecuteWithResult("DND Explosion Radius Retrieve", DND_USETARGET) * exprad_factor))
			TNT1 A 0 A_SetUserVar("user_fullexpradius", 0)
			TNT1 A 0 A_SetUserVar("user_damagetype", DND_DAMAGETYPE_EXPLOSIVES | (DND_DAMAGEFLAG_BLASTSELF << 16))
		Goto DoExplosionDamage
		ContinueFromExplosion:
			MISL B 8 Bright
			MISL C 6 Bright
			MISL D 4 Bright
		Stop
	}	
}

Actor NewRocket : RocketX {
	Speed 52
	SeeSound ""
	Decal "Scorch"
	Scale 1.15
	+DONTREFLECT
	var int user_exprad;
	States {
		SpawnState:
			MICM AA 1 A_JumpIfInTargetInventory("TorpedoBoomSignal", 1, "Boom")
			TNT1 A 0 A_SpawnItemEx("DnD_RocketTrail", -12, 0, 5)
		Loop
		XDeath:
			TNT1 A 0 ACS_NamedExecuteWithResult("DnD Do Impact Damage", ACS_NamedExecuteWithResult("DND Weapon Damage Retrieve", DND_DMGID_0 | (DND_WEAPON_TORPEDOLAUNCHER << 16), TALENT_EXPLOSIVE, DND_USETARGET | DND_ISSLOT5), DND_DAMAGETYPE_EXPLOSIVES, 0, DND_WEAPON_TORPEDOLAUNCHER)
		Death:
			TNT1 A 0 A_SpawnItemEx ("ExplosionFlare", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_NOCHECKPOSITION, 0)
			TNT1 AAAAA 0 A_CustomMissile("RocketSmoke", 0, 0, random(0, 360), 2, random(0, 180))
			TNT1 AAAAAAAA 0 A_CustomMissile("RocketSmoke", 0, 0, random(0, 360), 2, random(0, 360))
			TNT1 AAAAAAAAA 0 A_CustomMissile ("ExplosionParticleHeavy", 0, 0, random (0, 360), 2, random (0, 180))
			TNT1 AAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("ExplosionParticleHeavy", 0, 0, random (0, 360), 2, random (0, 360))
			TNT1 AAAAAAAAA 0 A_CustomMissile ("ExplosionParticleVeryFast", 0, 0, random (0, 360), 2, random (0, 360))
		
			TNT1 A 0 A_SetUserVar("user_expdmg", ACS_NamedExecuteWithResult("DND Weapon Damage Retrieve", DND_DMGID_1 | (DND_WEAPON_TORPEDOLAUNCHER << 16), TALENT_EXPLOSIVE, DND_USETARGET | DND_ISSLOT5))
			TNT1 A 0 A_SetUserVar("user_expradius", 128 * (1.0 + ACS_NamedExecuteWithResult("DND Explosion Radius Retrieve", DND_USETARGET) * exprad_factor))
			TNT1 A 0 A_SetUserVar("user_fullexpradius", 0)
			TNT1 A 0 A_SetUserVar("user_damagetype", DND_DAMAGETYPE_EXPLOSIVES | (DND_DAMAGEFLAG_BLASTSELF << 16))
		Goto DoExplosionDamage
		ContinueFromExplosion:
			MISL B 8 Bright
			MISL C 6 Bright
			MISL D 4 Bright
		Stop
		
		Boom:
			TNT1 A 0 A_Stop
			TNT1 A 0 A_ChangeVelocity(0, 0, 0, CVF_REPLACE)
		
			TNT1 A 0 A_SpawnItemEx ("ExplosionFlare", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_NOCHECKPOSITION, 0)
			TNT1 AAAAA 0 A_CustomMissile("RocketSmoke", 0, 0, random(0, 360), 2, random(0, 180))
			TNT1 AAAAAAAA 0 A_CustomMissile("RocketSmoke", 0, 0, random(0, 360), 2, random(0, 360))
			TNT1 AAAAAAAAA 0 A_CustomMissile ("ExplosionParticleHeavy", 0, 0, random (0, 360), 2, random (0, 180))
			TNT1 AAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("ExplosionParticleHeavy", 0, 0, random (0, 360), 2, random (0, 360))
			TNT1 AAAAAAAAA 0 A_CustomMissile ("ExplosionParticleVeryFast", 0, 0, random (0, 360), 2, random (0, 360))
			
			TNT1 A 0 A_SetUserVar("user_expdmg", ACS_NamedExecuteWithResult("DND Weapon Damage Retrieve", DND_DMGID_0 | (DND_WEAPON_TORPEDOLAUNCHER << 16), TALENT_EXPLOSIVE, DND_USETARGET | DND_ISSLOT5))
			TNT1 A 0 A_SetUserVar("user_expradius", 128 * (1.0 + ACS_NamedExecuteWithResult("DND Explosion Radius Retrieve", DND_USETARGET) * exprad_factor))
			TNT1 A 0 A_SetUserVar("user_fullexpradius", 0)
			TNT1 A 0 A_SetUserVar("user_damagetype", DND_DAMAGETYPE_EXPLOSIVES | (DND_DAMAGEFLAG_BLASTSELF << 16))
		Goto DoExplosionDamage
	}
}

Actor RocketShooter2 : DnD_Activator {
	States {
		Use:
			TNT1 A 0 ACS_NamedExecuteAlways("DnD Ammo Gain Chance", 0, DND_AMMOSLOT_ROCKET, AMMO_ROCKET, 1)
			TNT1 A 0 A_FireCustomMissile("NewRocket", 0, 1, 0, 0)
		Stop
	}
}

// Sprites by Eriance
Actor "Upgraded Rocket Launcher2" : RocketLauncher {	
	Weapon.Slotnumber 5
	Inventory.PickupSound "weapons/pickup"
	+INVENTORY.UNDROPPABLE
	Tag "$WEP_49_TAG"
    Weapon.BobStyle InverseSmooth
    Weapon.BobSpeed 1.6
    Weapon.BobRangeY 0.5
    Weapon.BobRangeX 0.8
	-NOAUTOFIRE
	States {
		Spawn:
			MERC X -1
		Stop
		Ready:
			MERC A 1 A_WeaponReady
		Loop
		Deselect:
			TNT1 A 0 A_StopSound(CHAN_WEAPON)
			TNT1 A 0 A_TakeInventory("H_WeaponSlot5", 1)
			MERC A 1 A_Lower
			TNT1 A 0 A_Lower
		Loop
		Select:
			TNT1 A 0 A_GiveInventory("H_WeaponSlot5", 1)
			TNT1 A 0 A_TakeInventory("DnD_WeaponID", 0x7FFFFFFF)
			TNT1 A 0 A_GiveInventory("DnD_WeaponID", DND_WEAPON_MERCURYLAUNCHER)
			TNT1 A 0 ACS_NamedExecuteWithResult("DND Weapon Damage Cache", DND_DMGID_0, 16, 16 | (20 << 16), DND_WEAPON_MERCURYLAUNCHER)
			TNT1 A 0 ACS_NamedExecuteWithResult("DND Weapon Damage Cache", DND_DMGID_1, 24, 0, DND_WEAPON_MERCURYLAUNCHER)
	    SelectLoop:
			MERC A 1 A_Raise
			TNT1 A 0 A_Raise
		Loop
		Fire:
			MERC A 1 A_CheckReload
			MERC B 1 Bright A_Gunflash
			MERC BC 1 Bright
			TNT1 A 0 A_PlayWeaponSound("weapons/mercuryfire")
			TNT1 A 0 Radius_Quake(4,3,0,1,0)
			MERC C 1 Bright A_GiveInventory("RocketShooter3", 1)
			MERC DEFGHI 1 Bright
			MERC JKLMN 3
			MERC A 15
			MERC A 6 A_Refire
		Goto Ready
		Flash:
			TNT1 A 3 A_Light1
			TNT1 A 4 A_Light2
			TNT1 A 3 A_Light1
			TNT1 A 2 A_Light0
		stop
	}
}

Actor MercurySmoke : RocketSmoke {
	Renderstyle Add
	Alpha 0.85
	
	States {
		Spawn:
			OBSK ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 Bright A_FadeOut(0.03)
		Stop
	}
}

Actor MercuryExplosion : DnD_ExplosiveBase {
	Speed 0
	+THRUACTORS
	States {
		SpawnState:
			TNT1 A 0
		Death:
			TNT1 A 0 A_SetArg(0, 8)
			TNT1 A 0 A_SetUserVar("user_expdmg", ACS_NamedExecuteWithResult("DND Weapon Damage Retrieve", DND_DMGID_1 | (DND_WEAPON_MERCURYLAUNCHER << 16), TALENT_EXPLOSIVE, DND_USETARGET | DND_ISSLOT5))
			TNT1 A 0 A_SetUserVar("user_expradius", 160 * (1.0 + ACS_NamedExecuteWithResult("DND Explosion Radius Retrieve", DND_USETARGET) * exprad_factor))
			TNT1 A 0 A_SetUserVar("user_fullexpradius", 0)
			TNT1 A 0 A_SetUserVar("user_damagetype", DND_DAMAGETYPE_EXPLOSIVES | (DND_DAMAGEFLAG_BLASTSELF << 16))
		Again:
			TNT1 A 1 
		Goto DoExplosionDamage
		ContinueFromExplosion:
			TNT1 A 7 A_SetArg(0, Args[0] - 1)
			TNT1 A 1 A_JumpIf(Args[0] > 0, "Again")
		Stop
	}
}

Actor MercuryRocket : RocketX {
	Speed 16
	SeeSound ""
	Decal "Scorch"
	Scale 1.1
	+LOOKALLAROUND
	+SCREENSEEKER
	+THRUGHOST
	+SEEKERMISSILE
	States {
		SpawnState:
			HMMS C 12
			TNT1 A 0 A_ChangeVelocity(velx * 2.75, vely * 2.75, velz * 2.75, CVF_REPLACE)
		SpawnLoop:
			HMMS A 2 Bright A_SeekerMissile(45, 15, SMF_LOOK | SMF_PRECISE | SMF_CURSPEED)
			TNT1 A 0 A_CustomMissile("MercurySmoke", 0, 0, random(225, 315), 2, random(0, 360))
			HMMS B 2 Bright A_SeekerMissile(45, 15, SMF_LOOK | SMF_PRECISE | SMF_CURSPEED)
			TNT1 A 0 A_CustomMissile("MercurySmoke", 0, 0, random(225, 315), 2, random(0, 360))
		Loop
		XDeath:
			TNT1 A 0 ACS_NamedExecuteWithResult("DnD Do Impact Damage", ACS_NamedExecuteWithResult("DND Weapon Damage Retrieve", DND_DMGID_0 | (DND_WEAPON_MERCURYLAUNCHER << 16), TALENT_EXPLOSIVE, DND_USETARGET | DND_ISSLOT5), DND_DAMAGETYPE_EXPLOSIVES, 0, DND_WEAPON_MERCURYLAUNCHER)
		Death:
			TNT1 A 0 A_SpawnItemEx ("ExplosionFlare", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_NOCHECKPOSITION, 0)
			TNT1 AAAAAAA 0 A_CustomMissile("MercurySmoke", 0, 0, random(0, 360), 2, random(0, 180))
			TNT1 AAAAAAAAAAAA 0 A_CustomMissile("MercurySmoke", 0, 0, random(0, 360), 2, random(0, 360))
			TNT1 AAAAAAAAA 0 A_CustomMissile ("ExplosionParticleHeavy", 0, 0, random (0, 360), 2, random (0, 180))
			TNT1 AAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("ExplosionParticleHeavy", 0, 0, random (0, 360), 2, random (0, 360))
			TNT1 AAAAAAAAA 0 A_CustomMissile ("ExplosionParticleVeryFast", 0, 0, random (0, 360), 2, random (0, 360))
			TNT1 A 0 A_CustomMissile("MercuryExplosion", 0, 0, 0, CMF_TRACKOWNER | CMF_AIMDIRECTION)
			MISL B 8 Bright
			TNT1 AAAAAAAAAAAA 0 A_CustomMissile("MercurySmoke", 0, 0, random(0, 360), 2, random(0, 360))
			MISL C 7 Bright
			TNT1 AAAAAAAAAAAA 0 A_CustomMissile("MercurySmoke", 0, 0, random(0, 360), 2, random(0, 360))
			MISL D 6 Bright
		Stop
	}
}

Actor RocketShooter3 : DnD_Activator {
	States {
		Use:
			TNT1 A 0 ACS_NamedExecuteAlways("DnD Ammo Gain Chance", 0, DND_AMMOSLOT_ROCKET, AMMO_ROCKET, 1)
			TNT1 A 0 A_GiveInventory("DnD_UsedNonSpecial", 1)
			TNT1 A 0 A_FireCustomMissile("MercuryRocket", 1, 1, 0, 0)
		Stop
	}
}

ACTOR "Upgraded Rocket Launcher3" : Weapon {
	Weapon.SelectionOrder 600
	Weapon.AmmoUse1 0
	Weapon.AmmoUse2 0
	Weapon.AmmoGive 0
	Weapon.AmmoType1 "FlakShell"
	Weapon.AmmoType2 "ShellSize_18"
	Weapon.SlotNumber 5
	Inventory.PickupMessage "$GOTSHOTGUN2"
	Inventory.PickupSound "weapons/pickup"
	Obituary "$OB_MPSSHOTGUN"
	Tag "$WEP_50_TAG"
	Decal "BulletChip"
	+INVENTORY.UNDROPPABLE
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 1.8
	Weapon.BobRangeY 0.7
	Weapon.BobRangeX 0.8
	States {
		Ready:
			TNT1 A 0 A_JumpIfInventory("VindicatorZoomed", 1, "ZoomReady")
			VS9G A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
		ZoomReady:
			V9ZG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
		Deselect:
			TNT1 A 0 A_JumpIfInventory("VindicatorZoomed", 1, "UnzoomDeselect")
			TNT1 A 0 A_TakeInventory("H_WeaponSlot5", 1)
		DeselectLoop:
			VS9G A 1 A_Lower
			TNT1 A 0 A_Lower
		Loop
		Select:
			TNT1 A 0 A_GiveInventory("H_WeaponSlot5", 1)
			TNT1 A 0 A_TakeInventory("DnD_WeaponID", 0x7FFFFFFF)
			TNT1 A 0 A_GiveInventory("DnD_WeaponID", DND_WEAPON_VINDICATOR)
			TNT1 A 0 ACS_NamedExecuteWithResult("DND Weapon Damage Cache", DND_DMGID_0, 5, 0, DND_WEAPON_VINDICATOR)
			TNT1 A 0 ACS_NamedExecuteWithResult("DND Weapon Damage Cache", DND_DMGID_1, 12, 0, DND_WEAPON_VINDICATOR)
			TNT1 A 0 ACS_NamedExecuteWithResult("DND Weapon Damage Cache", DND_DMGID_2, 16, 0, DND_WEAPON_VINDICATOR)
			TNT1 A 0 ACS_NamedExecuteWithResult("DND Weapon Damage Cache", DND_DMGID_3, 20, 0, DND_WEAPON_VINDICATOR)
		SelectLoop:
			VS9G A 1 A_Raise
			TNT1 A 0 A_Raise
		Loop
		Fire:
			TNT1 A 0 A_JumpIfInventory("ArtemisCheck", 1, "ContinueFire")
			TNT1 A 0 A_JumpIfInventory("ShellSize_18", 1, "ContinueFire")
		Goto Reload
		ContinueFire:
			TNT1 A 0 A_JumpIfInventory("VindicatorZoomed", 1, "FireZoom")
			VS9F A 2 Bright
			TNT1 A 0 A_PlaySound("Vindicator/Fire", CHAN_AUTO)
			TNT1 A 0 A_GunFlash
			TNT1 AAAAAA 0 A_FireCustomMissile("SGTracer", frandom(-6, 6), 0, 0, frandom(-6, 6))
			TNT1 A 0 A_SpawnItemEx("GunSmoke", 18 * cos(-pitch), 5, 34 * (1 + sin(-pitch)), 2, 0, 1)
			TNT1 A 0 A_GiveInventory("VindicatorShooter", 1)
			TNT1 A 0 A_SpawnItemEx ("SHOTGUNSMOKE", cos(-pitch)*36, random(5, 10), 36+(sin(-pitch)*20),0,0,2,0,0,0)
			VS9F B 1 A_SetPitch(pitch - 5.0)
			VS9F B 1 A_SetPitch(pitch + 1.0)
			VS9F B 1 A_SetPitch(pitch + 1.0)
			VS9F CC 1 A_SetPitch(pitch + 1.0)
			TNT1 A 0 A_SpawnItemEx ("FlakShellCasing",cos(-pitch)*28, 24, 30+(sin(-pitch)*36),0,random(5, 8), random(1, 8),0,0,0)
			TNT1 A 0 A_SpawnItemEx ("SHOTGUNSMOKE", cos(-pitch)*36, random(5, 10), 36+(sin(-pitch)*20),0,0,2,0,0,0)
			VS9F D 2
			VS9F EFG 2 A_SpawnItemEx ("SHOTGUNSMOKE", cos(-pitch)*36, random(5, 10), 36+(sin(-pitch)*20),0,0,2,0,0,0)
			VS9G A 2
			VS9G A 2 A_Refire
		Goto Ready
		FireZoom:
			V9ZF A 2 Bright
			TNT1 A 0 A_PlaySound("Vindicator/Fire", CHAN_AUTO)
			TNT1 A 0 A_GunFlash
			TNT1 AAAAAA 0 A_FireCustomMissile("SGTracer", frandom(-6, 6), 0, 0, frandom(-6, 6))
			TNT1 A 0 A_SpawnItemEx("GunSmoke", 18 * cos(-pitch), 5, 34 * (1 + sin(-pitch)), 2, 0, 1)
			TNT1 A 0 A_GiveInventory("VindicatorShooter2", 1)
			TNT1 A 0 A_SpawnItemEx ("SHOTGUNSMOKE", cos(-pitch)*36, 0, 36+(sin(-pitch)*20),0,0,2,0,0,0)
			V9ZF B 1 A_SetPitch(pitch - 3.0)
			V9ZF B 1 A_SetPitch(pitch + 1.0)
			V9ZF B 1 A_SetPitch(pitch + 1.0)
			V9ZF C 2 A_SetPitch(pitch + 1.0)
			TNT1 A 0 A_SpawnItemEx ("FlakShellCasing",cos(-pitch)*28, 24, 30+(sin(-pitch)*36),0,random(5, 8), random(1, 8),0,0,0)
			TNT1 A 0 A_SpawnItemEx ("SHOTGUNSMOKE", cos(-pitch)*36, 0, 36+(sin(-pitch)*20),0,0,2,0,0,0)
			V9ZF D 2
			V9ZF EFG 2 A_SpawnItemEx ("SHOTGUNSMOKE", cos(-pitch)*36, 0, 36+(sin(-pitch)*20),0,0,2,0,0,0)
			V9ZG A 4
		Goto Ready
		
		Reload:
			TNT1 A 0 A_JumpIfInventory("FlakShell", 1, "ContinueCheck")
			TNT1 A 0 A_JumpIfInventory("VindicatorZoomed", 1, "ZoomFailReload")
			VS9G A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
		Goto Ready
		ZoomFailReload:
			V9ZG A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
		Goto Ready
		
		ContinueCheck:
			TNT1 A 0 A_JumpIfInventory("ShellSize_18", 0, "NoReload")
			TNT1 A 0 A_JumpIfInventory("VindicatorZoomed", 1, "UnzoomReload")
		ReloadZoomCont:
			TNT1 A 0 A_JumpIfInventory("Ability_Reloader", 1, "DoReloadFaster")
		DoReload:
			VS9R ABCDE 2
			VS9R F 3 A_PlayWeaponSound("Vindicator/Clipout")
			VS9R GHIJK 2
			TNT1 A 0 A_FireCustomMissile("FlakClipDrop", frandom(-12.0, 12.0), 0, 0, 0)
			VS9R L 35
		ReloadLoop:
			TNT1 A 0 A_JumpIfInventory("FlakShell", 1, "ContinueReload")
		Goto FinishReload
		ContinueReload:
			TNT1 A 0 A_JumpIfInventory("ShellSize_18", 0, "FinishReload")
			TNT1 A 0 A_TakeInventory("FlakShell", 1)
			TNT1 A 0 A_GiveInventory("ShellSize_18", 1)
		Goto ReloadLoop
		FinishReload:
			VS9R MNOPQ 2
			VS9R R 3 A_PlayWeaponSound("Vindicator/Clipin")
			VS9R STUVWX 2
		Goto Ready

		DoReloadFaster:
			VS9R ABCDE 1
			VS9R F 2 A_PlayWeaponSound("Vindicator/Clipout")
			VS9R GHIJK 1
			TNT1 A 0 A_FireCustomMissile("FlakClipDrop", frandom(-12.0, 12.0), 0, 0, 0)
			VS9R L 17
		ReloadLoopFaster:
			TNT1 A 0 A_JumpIfInventory("FlakShell", 1, "ContinueReloadFaster")
		Goto FinishReloadFaster
		ContinueReloadFaster:
			TNT1 A 0 A_JumpIfInventory("ShellSize_18", 0, "FinishReloadFaster")
			TNT1 A 0 A_TakeInventory("FlakShell", 1)
			TNT1 A 0 A_GiveInventory("ShellSize_18", 1)
		Goto ReloadLoopFaster
		FinishReloadFaster:
			VS9R MNOPQ 1
			VS9R R 2 A_PlayWeaponSound("Vindicator/Clipin")
			VS9R STUVWX 1
		Goto Ready
		NoReload:
			VS9G A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
		Goto Ready
		
		AltFire:
			TNT1 A 0 A_JumpIfInventory("VindicatorZoomed", 1, "UnZoom")
			VS9Z A 1
			VS9Z B 1
			VS9Z C 1 A_ZoomFactor(2.0)
			TNT1 A 0 A_PlaySound("Weapons/Scope", CHAN_WEAPON)
			VS9Z D 1 A_GiveInventory("VindicatorZoomed", 1)
			VS9Z E 1 
			VS9Z F 1
		Goto Ready
		UnZoom:
			VS9Z F 1 A_ZoomFactor(1.0)
			TNT1 A 0 A_PlaySound("Weapons/Scope", CHAN_WEAPON)
			VS9Z E 1 A_TakeInventory("VindicatorZoomed", 1)
			VS9Z D 1
			VS9Z C 1
			VS9Z B 1
			VS9Z A 1
		Goto Ready
		UnZoomDeselect:
			VS9Z F 1 A_ZoomFactor(1.0)
			TNT1 A 0 A_PlaySound("Weapons/Scope", CHAN_WEAPON)
			VS9Z E 1 A_TakeInventory("VindicatorZoomed", 1)
			VS9Z D 1
			VS9Z C 1
			VS9Z B 1
			VS9Z A 1
		Goto DeselectLoop
		UnZoomReload:
			VS9Z F 1 A_ZoomFactor(1.0)
			TNT1 A 0 A_PlaySound("Weapons/Scope", CHAN_WEAPON)
			VS9Z E 1 A_TakeInventory("VindicatorZoomed", 1)
			VS9Z D 1
			VS9Z C 1
			VS9Z B 1
			VS9Z A 1
		Goto ReloadZoomCont

		Flash:
			TNT1 A 4 Bright A_Light1
			TNT1 A 3 Bright A_Light2
		Goto LightDone
		Spawn:
			VS9P X -1
		Stop
	}
}

Actor VindicatorZoomed : DnD_Boolean { }

Actor VindicatorShooter : DnD_Activator {
	States {
		Use:
			TNT1 A 0 ACS_NamedExecuteAlways("DnD Ammo Gain Chance", 0, DND_AMMOSLOT_MAGAZINE, DND_MAGAZINE_SHELLSIZE_18, 1)
			TNT1 A 0 A_GiveInventory("DnD_UsedNonSpecial", 1)
			TNT1 A 0 A_JumpIfInventory("ArtemisCheck", 1, "NoTake")
			TNT1 A 0 A_TakeInventory("ShellSize_18", 1)
		NoTake:
			TNT1 A 0 A_SetArg(DND_ARG_SHOTGUNPELLETS, ACS_NamedExecuteWithResult("DnD Pellet Count", 10))
			TNT1 A 0 ACS_NamedExecuteWithResult("DND Weapon Damage Retrieve", DND_DMGID_0 | (DND_WEAPON_VINDICATOR << 16), TALENT_BALLISTIC, DND_USETARGET | DND_ISBOOMSTICK | DND_ISSLOT5, Args[DND_ARG_SHOTGUNPELLETS])
			TNT1 A 0 A_JumpIfInventory("StatbuffCounter_PelletsInCircle", 1, "FireCircle")
			TNT1 A 0 A_FireBullets (10.4 * (1.0 - accuracy * accuracy_factor), 7.8 * (1.0 - accuracy * accuracy_factor), Args[DND_ARG_SHOTGUNPELLETS], 0, "FlakShellPuff", FBF_NORANDOM | FBF_USEAMMO, 320)
		Stop
		
		FireCircle:
			TNT1 A 0 A_JumpIfInventory("ArtemisCheck", 1, "NoTakeCircle")
			TNT1 A 0 A_TakeInventory("FlakShell", 1)
		NoTakeCircle:
			TNT1 A 0 A_SetArg(2, Args[DND_ARG_SHOTGUNPELLETS])
			TNT1 A 0 A_SetArg(4, Args[2])
		LoopStart:
			TNT1 A 0 A_JumpIf(Args[4], "LoopContinue")
		Goto Finish
		LoopContinue:
			TNT1 A 0 A_FireBullets((360.0 / Args[2]) * Args[4], 0, -1, 0, "FlakShellPuff", FBF_NORANDOM | FBF_EXPLICITANGLE, 320)
			TNT1 A 0 A_SetArg(4, Args[4] - 1)
		Goto LoopStart
		Finish:
			TNT1 A 0
		Stop
	}
}

Actor VindicatorShooter2 : DnD_Activator {
	States {
		Use:
			TNT1 A 0 ACS_NamedExecuteAlways("DnD Ammo Gain Chance", 0, DND_AMMOSLOT_MAGAZINE, DND_MAGAZINE_SHELLSIZE_18, 1)
			TNT1 A 0 A_GiveInventory("DnD_UsedNonSpecial", 1)
			TNT1 A 0 A_JumpIfInventory("ArtemisCheck", 1, "NoTake")
			TNT1 A 0 A_TakeInventory("ShellSize_18", 1)
		NoTake:
			TNT1 A 0 A_SetArg(DND_ARG_SHOTGUNPELLETS, ACS_NamedExecuteWithResult("DnD Pellet Count", 10))
			TNT1 A 0 A_JumpIfInventory("StatbuffCounter_PelletsInCircle", 1, "FireCircle")
			TNT1 A 0 A_FireBullets (5.2 * (1.0 - accuracy * accuracy_factor), 3.9 * (1.0 - accuracy * accuracy_factor), Args[DND_ARG_SHOTGUNPELLETS], 0, "FlakShellPuff", FBF_NORANDOM | FBF_USEAMMO, 320)
		Stop
		
		FireCircle:
			TNT1 A 0 A_JumpIfInventory("ArtemisCheck", 1, "NoTakeCircle")
			TNT1 A 0 A_TakeInventory("FlakShell", 1)
		NoTakeCircle:
			TNT1 A 0 A_SetArg(2, Args[DND_ARG_SHOTGUNPELLETS])
			TNT1 A 0 A_SetArg(4, Args[2])
		LoopStart:
			TNT1 A 0 A_JumpIf(Args[4], "LoopContinue")
		Goto Finish
		LoopContinue:
			TNT1 A 0 A_FireBullets((360.0 / Args[2]) * Args[4], 0, -1, 0, "FlakShellPuff", FBF_NORANDOM | FBF_EXPLICITANGLE, 320)
			TNT1 A 0 A_SetArg(4, Args[4] - 1)
		Goto LoopStart
		Finish:
			TNT1 A 0
		Stop
	}
}

Actor FlakShellPuff : DnD_BasePuff {
	Renderstyle "Add"
	Alpha 1.0
	Scale 0.5
	+ALWAYSPUFF
	States {
		Init:
			TNT1 A 0 A_SetArg(DND_ARG_DAMAGE, ACS_NamedExecuteWithResult("DND Weapon Damage Retrieve", DND_DMGID_0 | (DND_WEAPON_VINDICATOR << 16), TALENT_BALLISTIC, DND_USETARGET | DND_ISBOOMSTICK | DND_ISSLOT5))
			TNT1 A 0 A_SetArg(DND_ARG_DAMAGETYPE, DND_DAMAGETYPE_PHYSICAL)
			TNT1 A 0 A_SetArg(DND_ARG_DAMAGEFLAGS, DND_DAMAGEFLAG_ISSHOTGUN | DND_DAMAGEFLAG_ISHITSCAN)
			TNT1 A 0 A_SetArg(DND_ARG_WEAPONID, DND_WEAPON_VINDICATOR)
		Goto DoDamage
		AfterDamage:
			FP97 A 2 Bright A_PlaySound("Vindicator/PuffExp")
			TNT1 A 0 A_CustomMissile("FlakShellExplosion", 0, 0, 0, CMF_AIMDIRECTION | CMF_TRACKOWNER)
			FP97 BCDEFGHI 2 Bright
		Stop
		Crash:
		Melee:
		HitNoBlood:
			TNT1 AAA 0 A_CustomMissile("FlakProjectile", 0, 0, 180 + frandom(-3.6, 3.6), CMF_SAVEPITCH | CMF_OFFSETPITCH | CMF_TRACKOWNER, 180 + frandom(-2.4, 2.4))
		Goto AfterDamage
	}
}

Actor FlakShellExplosion : DnD_ExplosiveBase {
	Speed 0
	+THRUACTORS
	States {
		SpawnState:
			TNT1 A 0
		Death:
			TNT1 A 0 A_SetUserVar("user_expdmg", ACS_NamedExecuteWithResult("DND Weapon Damage Retrieve", DND_DMGID_3 | (DND_WEAPON_VINDICATOR << 16), TALENT_EXPLOSIVE, DND_ISBOOMSTICK | DND_USETARGET | DND_ISSLOT5))
			TNT1 A 0 A_SetUserVar("user_expradius", 96 * (1.0 + ACS_NamedExecuteWithResult("DND Explosion Radius Retrieve", DND_USETARGET) * exprad_factor))
			TNT1 A 0 A_SetUserVar("user_fullexpradius", 0)
			TNT1 A 0 A_SetUserVar("user_damagetype", DND_DAMAGETYPE_EXPLOSIVES | (DND_DAMAGEFLAG_BLASTSELF << 16))
			TNT1 A 1 
		Goto DoExplosionDamage
		ContinueFromExplosion:
			TNT1 A 1
		Stop
	}
}

ACTOR FlakProjectile : DnD_BaseFastProjectile {
	Height 4
	Radius 2
	RenderStyle "Add"
	Alpha 1.0
	Speed 48
	DeathSound "Vindicator/FlakExp"
	MissileType "FlakProjectileTrail"
	MissileHeight 4
	ReactionTime 8
	+THRUACTORS
	+THRUGHOST
	States {
		Spawn:
			TNT1 A 1 NoDelay A_ScaleVelocity(0.1)
			TNT1 A 0 A_GiveInventory("DnD_Projectile_SpawnChecks", 1)
			TNT1 A 0 A_ChangeFlag("THRUACTORS", 0)
			TNT1 A 0 A_ScaleVelocity(1.0)
		SpawnLoop:
			TNT1 A 0 A_CountDown
			DPTC A 1 Bright
		Loop
		XDeath:
			TNT1 A 0 ACS_NamedExecuteWithResult("DnD Do Impact Damage", ACS_NamedExecuteWithResult("DND Weapon Damage Retrieve", DND_DMGID_1 | (DND_WEAPON_VINDICATOR << 16), TALENT_EXPLOSIVE, DND_ISBOOMSTICK | DND_USETARGET | DND_ISSLOT5), DND_DAMAGETYPE_EXPLOSIVES, 0, DND_WEAPON_VINDICATOR)
		Death:
			TNT1 A 0 A_Stop
			TNT1 A 0 A_Jump(256, "1", "2", "3", "4")
		1:
			TNT1 A 2 A_CustomMissile("FlakProjectileExplosion1", 0, 0, 0, CMF_TRACKOWNER | CMF_AIMDIRECTION)
		Stop
		2:
			TNT1 A 2 A_CustomMissile("FlakProjectileExplosion2", 0, 0, 0, CMF_TRACKOWNER | CMF_AIMDIRECTION)
		Stop
		3:
			TNT1 A 2 A_CustomMissile("FlakProjectileExplosion3", 0, 0, 0, CMF_TRACKOWNER | CMF_AIMDIRECTION)
		Stop
		4:
			TNT1 A 2 A_CustomMissile("FlakProjectileExplosion4", 0, 0, 0, CMF_TRACKOWNER | CMF_AIMDIRECTION)
		Stop
	}
}

Actor FlakProjectileExplosion1 : DnD_ExplosiveBase {
	Speed 0
	Renderstyle "Add"
	Alpha 0.925
	Scale 0.35
	+THRUACTORS
	States {
		SpawnState:
			TNT1 A 0 A_SetUserVar("user_expdmg", ACS_NamedExecuteWithResult("DND Weapon Damage Retrieve", DND_DMGID_2 | (DND_WEAPON_VINDICATOR << 16), TALENT_EXPLOSIVE, DND_ISBOOMSTICK | DND_USETARGET | DND_ISSLOT5))
			TNT1 A 0 A_SetUserVar("user_expradius", 96 * (1.0 + ACS_NamedExecuteWithResult("DND Explosion Radius Retrieve", DND_USETARGET) * exprad_factor))
			TNT1 A 0 A_SetUserVar("user_fullexpradius", 0)
			TNT1 A 0 A_SetUserVar("user_damagetype", DND_DAMAGETYPE_EXPLOSIVES | (DND_DAMAGEFLAG_BLASTSELF << 16))
		Goto DoExplosionDamage
		ContinueFromExplosion:
			DEXP ABCDEFGHIJK 2 Bright
		Stop
	}
}

Actor FlakProjectileExplosion2 : FlakProjectileExplosion1 {
	States {
		ContinueFromExplosion:
			DXP2 ABCDEFGHIJKLMNOPQR 2 Bright
		Stop
	}
}

Actor FlakProjectileExplosion3 : FlakProjectileExplosion1 {
	States {
		ContinueFromExplosion:
			DXP3 ABCDEFGHIJKLMNOP 2 Bright
		Stop
	}
}

Actor FlakProjectileExplosion4 : FlakProjectileExplosion1 {
	States {
		ContinueFromExplosion:
			DXP4 ABCDEFGHIJKLMNOPQ 2 Bright
		Stop
	}
}

Actor FlakProjectileTrail {
	RenderStyle Add
	Alpha 1.0
	+NOBLOCKMONST
	+NOTELEPORT
	+FORCEXYBILLBOARD
	+NOBLOCKMAP
	+NOGRAVITY
	+THRUACTORS
	+CLIENTSIDEONLY
	States {
		Spawn:
			DPTC BCDEFGHIJ 1 Bright
		Stop
	}
}

// Sprites by Eriance
Actor "Grenade Launcher" : RocketLauncher {
	Weapon.Slotnumber 5
    Inventory.PickupSound "weapons/pickup"
    Inventory.PickupMessage "You got the grenade launcher!"
	Weapon.AmmoType "Grenades"
	Weapon.AmmoGive 3
	Weapon.AmmoUse 0
	Obituary "%o was blown away by %k's grenade launcher!"
	+NOAUTOAIM
	+INVENTORY.UNDROPPABLE
	Tag "$WEP_56_TAG"
    Weapon.BobStyle InverseSmooth
    Weapon.BobSpeed 1.6
    Weapon.BobRangeY 0.5
    Weapon.BobRangeX 0.9
   States
   {
   Spawn:
      NGLP A -1
      Stop
   Ready:
      NGLN A 1 A_WeaponReady
      Loop
   Deselect:
	  TNT1 A 0 A_TakeInventory("H_WeaponSlot5", 1)
      NGLN A 1 A_Lower
	  TNT1 A 0 A_Lower
      Loop
   Select:
	  TNT1 A 0 A_GiveInventory("H_WeaponSlot5", 1)
	  TNT1 A 0 A_TakeInventory("DnD_WeaponID", 0x7FFFFFFF)
	  TNT1 A 0 A_GiveInventory("DnD_WeaponID", DND_WEAPON_GRENADELAUNCHER)
	  TNT1 A 0 ACS_NamedExecuteWithResult("DND Weapon Damage Cache", DND_DMGID_1, 128, 0, DND_WEAPON_GRENADELAUNCHER)
	  TNT1 A 0 ACS_NamedExecuteWithResult("DND Weapon Damage Cache", DND_DMGID_2, 80, 0, DND_WEAPON_GRENADELAUNCHER)
   SelectLoop:
      NGLN A 1 A_Raise
	  TNT1 A 0 A_Raise
      Loop
   Fire:
	  TNT1 A 0 A_JumpIfInventory("SpecialAmmoMode_5X", AMMO_40MMSONIC, "ContinueFire")
	  TNT1 A 0 A_JumpIfInventory("ArtemisCheck", 1, "ContinueFire")
	  TNT1 A 0 A_JumpIfInventory("Grenades", 1, "ContinueFire")
	  NGLN A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
   Goto Ready
   ContinueFire:
      NGLN D 3 Bright A_GunFlash
	  TNT1 A 0 A_PlayWeaponSound("weapons/glauncherfire")
      NGLN E 3 Bright A_GiveInventory("GrenadeShooterGL", 1)
      NGLN FGH 3 Bright
	  NGLN CI 2
	  NGLN B 2
	  NGLN A 4
      NGLN A 4 A_Refire
      Goto Ready
   Flash:
      TNT1 A 3 A_Light1
      TNT1 A 4 A_Light2
      TNT1 A 3 A_Light1
      TNT1 A 2 A_Light0
      stop
	Altfire:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("DnD Weapon Can Switch Mode", AMMO_TYPE_GRENADE, SPWEP_GL), "ModeSwitch")
		NGLN A 2 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
	Goto Ready
	ModeSwitch:
		TNT1 A 0 A_JumpIfInventory("AmmoSwitchCooldown", 1, "NoAction")
		NGLN A 1 Offset(0, 41)
		TNT1 A 0 A_GiveInventory("AmmoSwitchCooldown", 1)
		TNT1 A 0 ACS_ExecuteAlways(DND_SPECIALAMMO_SCRIPT, 0, AMMO_TYPE_GRENADE, SPWEP_GL)
		NGLN A 1 Offset(0, 50)
		TNT1 A 0 ACS_ExecuteAlways(DND_SPECIALAMMOMSG_SCRIPT, 0)
		NGLN A 1 Offset(0, 59)
		NGLN A 1 Offset(0, 68)
		NGLN A 12 Offset(0, 77)
		NGLN A 1 Offset(0, 68)
		NGLN A 1 Offset(0, 56)
		NGLN A 1 Offset(0, 44)
		NGLN A 1 Offset(0, 32)
	Goto Ready
	NoAction:
		NGLN A 1 A_WeaponReady(WRF_NOSWITCH | WRF_NOFIRE)
	Goto Ready
   }
}

Actor "Upgraded Grenade Launcher" : RocketLauncher
{	
	Weapon.Slotnumber 5
    Inventory.PickupSound "weapons/pickup"
    Inventory.PickupMessage "You got the grenade launcher!"
	Weapon.AmmoType "Grenades"
	Weapon.AmmoGive 3
	Obituary "%o was blown away by %k's grenade launcher!"
	+NOAUTOAIM
	+INVENTORY.UNDROPPABLE
	Tag "$WEP_57_TAG"
	Weapon.BobStyle InverseSmooth
    Weapon.BobSpeed 1.6
    Weapon.BobRangeY 0.5
    Weapon.BobRangeX 0.9
   States
   {
   Spawn:
      RTGL Z -1
      Stop
   Ready:
      RTGL A 1 A_WeaponReady
      Loop
   Deselect:
      TNT1 A 0 A_TakeInventory("H_WeaponSlot5", 1)
      RTGL A 1 A_Lower
	  TNT1 A 0 A_Lower
      Loop
   Select:
      TNT1 A 0 A_GiveInventory("H_WeaponSlot5", 1)
	  TNT1 A 0 A_TakeInventory("DnD_WeaponID", 0x7FFFFFFF)
	  TNT1 A 0 A_GiveInventory("DnD_WeaponID", DND_WEAPON_ROTARYGRENADELAUNCHER)
	  TNT1 A 0 ACS_NamedExecuteWithResult("DND Weapon Damage Cache", DND_DMGID_0, 192, 0, DND_WEAPON_ROTARYGRENADELAUNCHER)
	  TNT1 A 0 ACS_NamedExecuteWithResult("DND Weapon Damage Cache", DND_DMGID_1, 384, 0, DND_WEAPON_ROTARYGRENADELAUNCHER)
   SelectLoop:
      RTGL A 1 A_Raise
	  TNT1 A 0 A_Raise
      Loop
   Fire:
      RTGL BC 1 Bright A_GunFlash
	  TNT1 A 0 A_PlayWeaponSound("weapons/glauncherfiren")
	  TNT1 A 0 Radius_Quake(4,3,0,1,0)
      RTGL D 1 Bright A_GiveInventory("GrenadeShooter2", 1)
	  RTGL EF 1 Bright
	  TNT1 A 0 A_CheckReload
	  RTGL G 1
	  TNT1 A 0 A_PlaySound("weapons/glauncherload")
      RTGL GHH 1
	  RTGL IJ 5
	  RTGL K 2
      RTGL A 5 A_Refire
      Goto Ready
   Flash:
      TNT1 A 3 A_Light1
      TNT1 A 4 A_Light2
      TNT1 A 3 A_Light1
      TNT1 A 2 A_Light0
      stop
   }
}

Actor GrenadeShooter2 : DnD_Activator {
	States {
		Use:
			TNT1 A 0 ACS_NamedExecuteAlways("DnD Ammo Gain Chance", 0, DND_AMMOSLOT_ROCKET, AMMO_GRENADE, 1)
			TNT1 A 0 A_GiveInventory("DnD_UsedNonSpecial", 1)
			TNT1 A 0 A_FireCustomMissile("NewGrenade", 0, 1, 0, 4)
		Stop
	}
}

Actor NormalGrenade : DnD_ExplosiveBase {
	speed 28
	BounceSound "weapons/grenadebounce"
	Scale 1.0
	Radius 8
	Height 16
	+DONTBLAST
	+EXPLODEONWATER
	+THRUGHOST
	-NOGRAVITY
	BounceType Doom
	Bouncecount 3
	BounceFactor 0.625
	Gravity 0.84
	DeathSound "Weapons/grenadexpl"
	States {
		SpawnState:
			NGLP B 5 A_SpawnItem("GrenadeSmoke")
		Loop
		XDeath:
			TNT1 A 0 ACS_NamedExecuteWithResult("DnD Do Impact Damage", ACS_NamedExecuteWithResult("DND Weapon Damage Retrieve", DND_DMGID_2 | (DND_WEAPON_GRENADELAUNCHER << 16), TALENT_EXPLOSIVE, DND_USETARGET | DND_ISSLOT5), DND_DAMAGETYPE_EXPLOSIVES, 0, DND_WEAPON_GRENADELAUNCHER)
		Death:
			TNT1 A 0 A_ChangeFlag("NOGRAVITY", 1)
			TNT1 A 0 A_SpawnItemEx ("ExplosionFlare", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_NOCHECKPOSITION, 0)
			TNT1 AAAAA 0 A_CustomMissile("RocketSmoke", 0, 0, random(0, 360), 2, random(0, 180))
			TNT1 AAAAAAAA 0 A_CustomMissile("RocketSmoke", 0, 0, random(0, 360), 2, random(0, 360))
			TNT1 AAAAAAAAA 0 A_CustomMissile ("ExplosionParticleHeavy", 0, 0, random (0, 360), 2, random (0, 180))
			TNT1 AAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("ExplosionParticleHeavy", 0, 0, random (0, 360), 2, random (0, 360))
			TNT1 AAAAAAAAA 0 A_CustomMissile ("ExplosionParticleVeryFast", 0, 0, random (0, 360), 2, random (0, 360))
			
			TNT1 A 0 A_SetUserVar("user_expdmg", ACS_NamedExecuteWithResult("DND Weapon Damage Retrieve", DND_DMGID_1 | (DND_WEAPON_GRENADELAUNCHER << 16), TALENT_EXPLOSIVE, DND_USETARGET | DND_ISSLOT5))
			TNT1 A 0 A_SetUserVar("user_expradius", 144 * (1.0 + ACS_NamedExecuteWithResult("DND Explosion Radius Retrieve", DND_USETARGET) * exprad_factor))
			TNT1 A 0 A_SetUserVar("user_fullexpradius", 0)
			TNT1 A 0 A_SetUserVar("user_damagetype", DND_DAMAGETYPE_EXPLOSIVES | (DND_DAMAGEFLAG_BLASTSELF << 16))
		Goto DoExplosionDamage
		ContinueFromExplosion:
			MISL B 8 Bright
			MISL C 6 Bright
			MISL D 4 Bright	
		Stop
	}
}

Actor NewGrenade : NormalGrenade {
	Scale 0.55
	DeathSound "Weapons/grenadexpl2"
	States {
		SpawnState:
			NGLP C 5 A_SpawnItem("GrenadeSmoke")
		Loop
		XDeath:
			TNT1 A 0 ACS_NamedExecuteWithResult("DnD Do Impact Damage", ACS_NamedExecuteWithResult("DND Weapon Damage Retrieve", DND_DMGID_1 | (DND_WEAPON_ROTARYGRENADELAUNCHER << 16), TALENT_EXPLOSIVE, DND_USETARGET | DND_ISSLOT5), DND_DAMAGETYPE_EXPLOSIVES, 0, DND_WEAPON_ROTARYGRENADELAUNCHER)
		Death:
			TNT1 A 0 A_ChangeFlag("NOGRAVITY", 1)
			TNT1 A 0 A_SpawnItemEx ("ExplosionFlare", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_NOCHECKPOSITION, 0)
			TNT1 AAAAA 0 A_CustomMissile("RocketSmoke", 0, 0, random(0, 360), 2, random(0, 180))
			TNT1 AAAAAAAA 0 A_CustomMissile("RocketSmoke", 0, 0, random(0, 360), 2, random(0, 360))
			TNT1 AAAAAAAAA 0 A_CustomMissile ("ExplosionParticleHeavy", 0, 0, random (0, 360), 2, random (0, 180))
			TNT1 AAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("ExplosionParticleHeavy", 0, 0, random (0, 360), 2, random (0, 360))
			TNT1 AAAAAAAAA 0 A_CustomMissile ("ExplosionParticleVeryFast", 0, 0, random (0, 360), 2, random (0, 360))
			
			TNT1 A 0 A_SetUserVar("user_expdmg", ACS_NamedExecuteWithResult("DND Weapon Damage Retrieve", DND_DMGID_0 | (DND_WEAPON_ROTARYGRENADELAUNCHER << 16), TALENT_EXPLOSIVE, DND_USETARGET | DND_ISSLOT5))
			TNT1 A 0 A_SetUserVar("user_expradius", 192 * (1.0 + ACS_NamedExecuteWithResult("DND Explosion Radius Retrieve", DND_USETARGET) * exprad_factor))
			TNT1 A 0 A_SetUserVar("user_fullexpradius", 0)
			TNT1 A 0 A_SetUserVar("user_damagetype", DND_DAMAGETYPE_EXPLOSIVES | (DND_DAMAGEFLAG_BLASTSELF << 16))
		Goto DoExplosionDamage
		ContinueFromExplosion:
			TNT1 A 1 A_SpawnItem("NewGrenadeExplosion")
		Stop
	}
}

// Another drop only weapon, Sprites by Eriance
ACTOR "Heavy Missile Launcher" : Weapon
{
   Inventory.PickupSound "weapons/pickup"
   Inventory.PickupMessage "You got the Heavy Missile Launcher!"
   Obituary "%o couldn't dodge %k's missiles."
   Weapon.AmmoType "MISAmmo"
   Weapon.AmmoType2 "MISAmmo"
   Weapon.AmmoGive 2
   Weapon.AmmoUse1 1
   Weapon.AmmoUse2 1
   Weapon.Slotnumber 5
   Weapon.SelectionOrder 1600
   +INVENTORY.UNDROPPABLE
	Weapon.BobStyle InverseSmooth
    Weapon.BobSpeed 1.6
    Weapon.BobRangeY 0.5
    Weapon.BobRangeX 0.9
	Tag "$WEP_58_TAG"
   States
   {
   Spawn:
      WMLN A -1
      Loop
   Ready:
      MLNG A 1 A_WeaponReady
      Loop
   Deselect:
      TNT1 A 0 A_TakeInventory("H_WeaponSlot5", 1)
      MLNG A 1 A_Lower
	  TNT1 A 0 A_Lower
      Loop
   Select:
      TNT1 A 0 A_GiveInventory("H_WeaponSlot5", 1)
	  TNT1 A 0 A_TakeInventory("DnD_WeaponID", 0x7FFFFFFF)
	  TNT1 A 0 A_GiveInventory("DnD_WeaponID", DND_WEAPON_HEAVYMISSILELAUNCHER)
	  TNT1 A 0 ACS_NamedExecuteWithResult("DND Weapon Damage Cache", DND_DMGID_0, 192, 0, DND_WEAPON_HEAVYMISSILELAUNCHER)
	SelectLoop:
      MLNG A 1 A_Raise
	  TNT1 A 0 A_Raise
      Loop
   Fire:
      MLNG A 4
      MLNG D 0 Bright A_GunFlash
      MLNG D 4 Bright
	  TNT1 A 0 A_PlayWeaponSound("weapons/heavyrlfire")
	  TNT1 A 0 Radius_Quake(4,3,0,1,0)
      MLNG E 3 Bright A_GiveInventory("MissileShooter", 1)
	  MLNG FGHCB 3 Bright
	  MLNG A 2 
      MLNG A 2 A_Refire  
      Goto Ready
   AltFire:
      MLNG A 4
      MLNG D 0 Bright A_GunFlash
      MLNG D 4 Bright
	  TNT1 A 0 A_PlayWeaponSound("weapons/heavyrlfire")
	  TNT1 A 0 Radius_Quake(4,3,0,1,0)
      MLNG E 3 Bright A_GiveInventory("MissileShooter2", 1)
	  MLNG FGHCB 3 Bright
	  MLNG A 2 
      MLNG A 2 A_Refire  
      Goto Ready
   Flash:
      TNT1 A 5 A_Light1
      TNT1 A 6 A_Light2
      TNT1 A 5 A_Light1
      TNT1 A 0 A_Light0
      stop
   }
}

ACTOR HomRocket1 : DnD_ExplosiveBase {
	Radius 8
	Height 11
	Speed 32
	DeathSound "weapons/heavyrlexp"
	+SeekerMissile
	+Rockettrail
	+THRUGHOST
	+FORCEXYBILLBOARD
	+EXPLODEONWATER
	+DONTBLAST
	SeeSound ""
	States {
		SpawnState:
			HMIS AA 4 Bright
		SpawnLoop:
			TNT1 A 0 A_SpawnItemEx("DnD_RocketTrail", -12, 0, 10)
			HMIS A 2 Bright A_SeekerMissile (10,25)
			HMIS A 2 Bright A_SpawnItemEx("DnD_RocketTrail", -12, 0, 10)
		Loop
		XDeath:
			TNT1 A 0 ACS_NamedExecuteWithResult("DnD Do Impact Damage", ACS_NamedExecuteWithResult("DND Weapon Damage Retrieve", DND_DMGID_0 | (DND_WEAPON_HEAVYMISSILELAUNCHER << 16), TALENT_EXPLOSIVE, DND_USETARGET | DND_ISSLOT5), DND_DAMAGETYPE_EXPLOSIVES, 0, DND_WEAPON_HEAVYMISSILELAUNCHER)
		Death:
			TNT1 A 0 A_SetTranslucent (0.75,1)
			TNT1 A 0 A_SpawnItemEx ("ExplosionFlare", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_NOCHECKPOSITION, 0)
			TNT1 AAAAA 0 A_CustomMissile("RocketSmoke", 0, 0, random(0, 360), 2, random(0, 180))
			TNT1 AAAAAAAA 0 A_CustomMissile("RocketSmoke", 0, 0, random(0, 360), 2, random(0, 360))	
			TNT1 AAAAAAAAA 0 A_CustomMissile ("ExplosionParticleHeavy", 0, 0, random (0, 360), 2, random (0, 180))
			TNT1 AAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("ExplosionParticleHeavy", 0, 0, random (0, 360), 2, random (0, 360))
			TNT1 AAAAAAAAA 0 A_CustomMissile ("ExplosionParticleVeryFast", 0, 0, random (0, 360), 2, random (0, 360))
			
			TNT1 A 0 A_SetUserVar("user_expdmg", ACS_NamedExecuteWithResult("DND Weapon Damage Retrieve", DND_DMGID_0 | (DND_WEAPON_HEAVYMISSILELAUNCHER << 16), TALENT_EXPLOSIVE, DND_USETARGET | DND_ISSLOT5))
			TNT1 A 0 A_SetUserVar("user_expradius", 160 * (1.0 + ACS_NamedExecuteWithResult("DND Explosion Radius Retrieve", DND_USETARGET) * exprad_factor))
			TNT1 A 0 A_SetUserVar("user_fullexpradius", 0)
			TNT1 A 0 A_SetUserVar("user_damagetype", DND_DAMAGETYPE_EXPLOSIVES | ((DND_DAMAGEFLAG_BLASTSELF | DND_DAMAGEFLAG_FORCERADIUSDMG) << 16))
		Goto DoExplosionDamage
		ContinueFromExplosion:
			MACX A 2 Bright
			MACX BCDEFGHIJ 2 Bright
		stop
	}
}

Actor HomRocket1NH : HomRocket1 {
	-SEEKERMISSILE
}

Actor MissileShooter : DnD_Activator {
	States {
		Use:
			TNT1 A 0 ACS_NamedExecuteAlways("DnD Ammo Gain Chance", 0, DND_AMMOSLOT_ROCKET, AMMO_HMISSILE, 2)
			TNT1 A 0 A_GiveInventory("DnD_UsedNonSpecial", 1)
	        TNT1 A 0 A_FireCustomMissile ("HomRocket1",-4,1,0,0)
			TNT1 A 0 A_FireCustomMissile ("HomRocket1",4,1,0,0)
		Stop
	}
}

Actor MissileShooter2 : DnD_Activator {
	States {
		Use:
			TNT1 A 0 ACS_NamedExecuteAlways("DnD Ammo Gain Chance", 0, DND_AMMOSLOT_ROCKET, AMMO_HMISSILE, 2)
			TNT1 A 0 A_GiveInventory("DnD_UsedNonSpecial", 1)
	        TNT1 A 0 A_FireCustomMissile ("HomRocket1NH", 0,1,-8,0)
			TNT1 A 0 A_FireCustomMissile ("HomRocket1NH", 0,1,8,0)
		Stop
	}
}

// Sprites by Yholl
ACTOR "Sedrin Staff" : Weapon {
	Inventory.PickupSound "weapons/pickup"
	Inventory.PickupMessage "You got the Sedrin Staff!"
	Obituary "%o couldn't dodge %k's missiles."
	Weapon.AmmoType "SedrinAmmo"
	Weapon.AmmoType2 "SedrinAmmo"
	Weapon.AmmoGive 5
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 1
	Weapon.Slotnumber 5
	Weapon.SelectionOrder 1000
	+INVENTORY.UNDROPPABLE
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 1.6
	Weapon.BobRangeY 0.4
	Weapon.BobRangeX 0.7
	Tag "$WEP_59_TAG"
	+WEAPON.NOAUTOAIM
	States {
		Spawn:
			TNT1 A 1
		Stop
	
		Ready:
			TNT1 A 0 A_PlaySound("SedrinStaff/idle", 7, 1.0, 1, 1.5)
			ARO2 AABBCCDDEEFF 1 A_WeaponReady
		Loop

		Select:
			TNT1 A 0 A_PlaySound("SedrinStaff/idle", 7, 1.0, 1, 1.5)
			TNT1 A 0 A_GiveInventory("H_WeaponSlot5", 1)
			TNT1 A 0 A_GiveInventory("DnD_UsingEnergy", 1)
			TNT1 A 0 A_TakeInventory("DnD_WeaponID", 0x7FFFFFFF)
			TNT1 A 0 A_GiveInventory("DnD_WeaponID", DND_WEAPON_SEDRINSTAFF)
			TNT1 A 0 ACS_NamedExecuteWithResult("DND Weapon Damage Cache", DND_DMGID_0, 5, 15 | (20 << 16), DND_WEAPON_SEDRINSTAFF)
			TNT1 A 0 ACS_NamedExecuteWithResult("DND Weapon Damage Cache", DND_DMGID_1, 96, 0, DND_WEAPON_SEDRINSTAFF)
			AROK T 1 A_Raise
			"####" "#" 0 A_Raise
			AROK T 1 A_Raise
			"####" "#" 0 A_Raise
		SelectLoop:
			AROK U 1 A_Raise
			"####" "#" 0 A_Raise
		Loop
		Deselect:
			TNT1 A 0 A_StopSound(7)
			TNT1 A 0 A_TakeInventory("H_WeaponSlot5", 1)
			TNT1 A 0 A_TakeInventory("DnD_UsingEnergy", 1)
			AROK U 1 A_Lower
			"####" "#" 0 A_Lower
			AROK U 1 A_Lower
			"####" "#" 0 A_Lower
		DeselectLoop:
			AROK T 1 A_Lower
			"####" "#" 0 A_Lower
		Loop

		Dryfire:
			TNT1 A 0 A_StopSound(CHAN_7)
			AROK TUCCBABCCUT 1
		Goto Ready

		Fire:
			TNT1 A 0 A_JumpIfInventory ("SedrinAmmo", 1, 1)
		Goto Dryfire
			AROK RSD 1
			AROK EFG 2
			TNT1 A 0 A_GunFlash ("Flash")
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_StopSound(CHAN_7)
			AROK HIJK 1 Bright
			TNT1 A 0 A_GunFlash ("FlashFire")
			TNT1 A 0 A_GiveInventory("SedrinShooter", 1)
			AROK L 1 A_SetPitch(pitch - 1.65)
			AROK L 1 A_SetPitch(pitch - 0.5)
			AROK L 1 A_SetPitch(pitch + 0.05)
			AROK L 1 A_SetPitch(pitch + 0.25)
			AROK M 1 A_SetPitch(pitch + 0.5)
			AROK M 1 A_SetPitch(pitch + 0.39)
			AROK N 1 A_SetPitch(pitch + 0.31)
			AROK N 1 A_SetPitch(pitch + 0.25)
			AROK O 1 A_SetPitch(pitch + 0.19)
			AROK O 1 A_SetPitch(pitch + 0.13)
			AROK P 1 A_SetPitch(pitch + 0.07)
			AROK P 1 A_SetPitch(pitch + 0.01)
			AROK Q 2
			ARO2 AABBCCDDEEFF 1 A_WeaponReady (WRF_NOBOB)
		Goto Ready
		
		AltFire:
			AROK RSD 1
			AROK EFG 2
			TNT1 A 0 A_GunFlash ("Flash")
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_StopSound(CHAN_7)
			AROK HIJK 1 Bright
			TNT1 A 0 A_GunFlash ("FlashFire")
			TNT1 A 0 A_GiveInventory("SedrinShooter2", 1)
			AROK L 1 A_SetPitch(pitch - 1.65)
			AROK L 1 A_SetPitch(pitch - 0.5)
			AROK L 1 A_SetPitch(pitch + 0.05)
			AROK L 1 A_SetPitch(pitch + 0.25)
			AROK M 1 A_SetPitch(pitch + 0.5)
			AROK M 1 A_SetPitch(pitch + 0.39)
			AROK N 1 A_SetPitch(pitch + 0.31)
			AROK N 1 A_SetPitch(pitch + 0.25)
			AROK O 1 A_SetPitch(pitch + 0.19)
			AROK O 1 A_SetPitch(pitch + 0.13)
			AROK P 1 A_SetPitch(pitch + 0.07)
			AROK P 1 A_SetPitch(pitch + 0.01)
			AROK Q 2
			ARO2 AABBCCDDEEFF 1 A_WeaponReady (WRF_NOBOB)
		Goto Ready

		Flash:
			TNT1 AAAA 1 A_Light (random(1,4))
			TNT1 A 0 A_Light0
		Goto LightDone
		FlashFire:
			TNT1 A 1 A_Light (20)
			TNT1 A 1 A_Light (10)
			TNT1 A 1 A_Light (5)
			TNT1 A 1 A_Light (2)
			TNT1 A 1 A_Light (1)
			TNT1 A 0 A_Light0
		Goto LightDone
	}
}

Actor SedrinShooter : DnD_Activator {
	States {
		Pickup:
			TNT1 A 0 ACS_NamedExecuteAlways("DnD Ammo Gain Chance", 0, DND_AMMOSLOT_ROCKET, AMMO_SEDRIN, 1)
			TNT1 A 0 A_PlayWeaponSound("SedrinStaff/Fire")
			TNT1 A 0 A_GiveInventory("DnD_UsedNonSpecial", 1)
			TNT1 A 0 A_FireCustomMissile("SedrinProjectile", 0, 1, 0, 0)
		Stop
	}
}

Actor SedrinShooter2 : DnD_Activator {
	States {
		Pickup:
			TNT1 A 0 ACS_NamedExecuteAlways("DnD Ammo Gain Chance", 0, DND_AMMOSLOT_ROCKET, AMMO_SEDRIN, 1)
			TNT1 A 0 A_PlayWeaponSound("SedrinStaff/Fire")

			TNT1 A 0 A_JumpIfInventory("StatbuffCounter_PelletsInCircle", 1, "FireCircle")
			TNT1 A 0 A_FireCustomMissile("SedrinSplitProjectile", -9.25, 1, 0, 0)
			TNT1 A 0 A_FireCustomMissile("SedrinSplitProjectile", 9.25, 0, 0, 0)
			TNT1 A 0 A_FireCustomMissile("SedrinSplitProjectile", 0, 0, 0, 0)
		Stop
		
		FireCircle:
			TNT1 A 0 A_SetArg(DND_ARG_SHOTGUNPELLETS, ACS_NamedExecuteWithResult("DnD Pellet Count", 3))
			TNT1 A 0 A_SetArg(DND_ARG_SHOTGUNTEMP, Args[DND_ARG_SHOTGUNPELLETS])
			TNT1 A 0 A_TakeInventory("SedrinAmmo", 1)
		LoopStart:
			TNT1 A 0 A_JumpIf(Args[DND_ARG_SHOTGUNTEMP], "LoopContinue")
		Goto Finish
		LoopContinue:
			TNT1 A 0 A_FireCustomMissile("SedrinSplitProjectile", (360.0 / Args[DND_ARG_SHOTGUNPELLETS]) * Args[DND_ARG_SHOTGUNTEMP], 0)
			TNT1 A 0 A_SetArg(DND_ARG_SHOTGUNTEMP, Args[DND_ARG_SHOTGUNTEMP] - 1)
		Goto LoopStart
		Finish:
			TNT1 A 0
		Stop
	}
}

Actor SedrinProjectile : DnD_ExplosiveBase {
	DeathSound "SedrinStaff/Hit"
	Radius 12
	Height 24
	Speed 36
	Renderstyle Add
	Scale 1.5
	+FORCERADIUSDMG
	States {
		SpawnState:
			ACID A 2 Bright A_SpawnItemEx("SedrinTrail", -12, 0, 0)
		Loop
		XDeath:
			TNT1 A 0 ACS_NamedExecuteWithResult("DnD Do Impact Damage", ACS_NamedExecuteWithResult("DND Weapon Damage Retrieve", DND_DMGID_0 | (DND_WEAPON_SEDRINSTAFF << 16), TALENT_ENERGY, DND_USETARGET | DND_ISSLOT5), DND_DAMAGETYPE_ENERGY, DND_DAMAGEFLAG_INFLICTPOISON, DND_WEAPON_SEDRINSTAFF)
		Death:
			TNT1 A 0 A_SpawnItemEx ("GreenSmallFlash", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_NOCHECKPOSITION, 0)
			TNT1 AAAAAAAAA 0 A_SpawnItemEx("GreenParticleSlowVanish", 0, 0, 0, frandom(-1, 1), frandom(-1, 1), frandom(-1, 1))

			TNT1 A 0 A_SetUserVar("user_expdmg", ACS_NamedExecuteWithResult("DND Weapon Damage Retrieve", DND_DMGID_1 | (DND_WEAPON_SEDRINSTAFF << 16), TALENT_ENERGY, DND_USETARGET | DND_ISSLOT5))
			TNT1 A 0 A_SetUserVar("user_expradius", 128 * (1.0 + ACS_NamedExecuteWithResult("DND Explosion Radius Retrieve", DND_USETARGET) * exprad_factor))
			TNT1 A 0 A_SetUserVar("user_fullexpradius", 128)
			TNT1 A 0 A_SetUserVar("user_damagetype", DND_DAMAGETYPE_ENERGY | (DND_DAMAGEFLAG_BLASTSELF | DND_DAMAGEFLAG_INFLICTPOISON) << 16)
			TNT1 A 0 A_SetUserVar("user_wepid", DND_WEAPON_SEDRINSTAFF)
		Goto DoExplosionDamage
		ContinueFromExplosion:
			TNT1 A 0 A_SetScale(0.75, 0.75)
			BPLO ABC 2 Bright
			TNT1 A 0 A_CustomMissile("SedrinSplitProjectile", 0, 0, 0, CMF_TRACKOWNER | CMF_AIMDIRECTION)
			TNT1 A 0 A_CustomMissile("SedrinSplitProjectile", 0, 0, 60, CMF_TRACKOWNER | CMF_AIMDIRECTION)
			TNT1 A 0 A_CustomMissile("SedrinSplitProjectile", 0, 0, 120, CMF_TRACKOWNER | CMF_AIMDIRECTION)
			TNT1 A 0 A_CustomMissile("SedrinSplitProjectile", 0, 0, 180, CMF_TRACKOWNER | CMF_AIMDIRECTION)
			TNT1 A 0 A_CustomMissile("SedrinSplitProjectile", 0, 0, 240, CMF_TRACKOWNER | CMF_AIMDIRECTION)
			TNT1 A 0 A_CustomMissile("SedrinSplitProjectile", 0, 0, 300, CMF_TRACKOWNER | CMF_AIMDIRECTION)
			BPLO DEFGHIJKLM 2 Bright
		Stop
	}
}

Actor SedrinSplitProjectile : SedrinProjectile {
	Radius 6
	Height 12
	Speed 24
	Renderstyle Add
	Scale 0.5
	ReactionTime 4
	+THRUACTORS
	
	States {
		SpawnState:
			ACID AA 2 Bright A_SpawnItemEx("SedrinTrail_Small", -12, 0, 0)
		Goto ExplosionSetup
		Spawn2:
			ACID AA 2 Bright A_SpawnItemEx("SedrinTrail_Small", -12, 0, 0)
		Goto Boom
		Spawn3:
			ACID AA 2 Bright A_SpawnItemEx("SedrinTrail_Small", -12, 0, 0)
		Goto Boom
		Spawn4:
			ACID AA 2 Bright A_SpawnItemEx("SedrinTrail_Small", -12, 0, 0)
		Goto Boom
		XDeath:
		Death:
			TNT1 A 0 A_Stop
			TNT1 A 0 A_ChangeVelocity(0, 0, 0, CVF_REPLACE)
			TNT1 A 0 A_SpawnItemEx ("GreenSmallFlash_Smaller", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE | SXF_NOCHECKPOSITION, 0)
			TNT1 AA 0 A_SpawnItemEx("GreenParticleSlowVanish", 0, 0, 0, frandom(-1, 1), frandom(-1, 1), frandom(-1, 1))
		Stop
		ExplosionSetup:
			TNT1 A 0 A_SetUserVar("user_expdmg", 0.5 * ACS_NamedExecuteWithResult("DND Weapon Damage Retrieve", DND_DMGID_1 | (DND_WEAPON_SEDRINSTAFF << 16), TALENT_ELEMENTAL, DND_USETARGET | DND_ISSLOT5))
			TNT1 A 0 A_SetUserVar("user_expradius", 64 * (1.0 + ACS_NamedExecuteWithResult("DND Explosion Radius Retrieve", DND_USETARGET) * exprad_factor))
			TNT1 A 0 A_SetUserVar("user_fullexpradius", 64)
			TNT1 A 0 A_SetUserVar("user_damagetype", DND_DAMAGETYPE_POISON | (DND_DAMAGEFLAG_INFLICTPOISON << 16))
			TNT1 A 0 A_SetUserVar("user_wepid", DND_WEAPON_SEDRINSTAFF)
		Boom:
			TNT1 A 0 A_SpawnItemEx("SedrinSmallExplosion")
			TNT1 A 0 A_CountDown
		Goto DoExplosionDamage
		ContinueFromExplosion:
			TNT1 A 0 A_JumpIf(reactiontime == 3, "Spawn4")
			TNT1 A 0 A_JumpIf(reactiontime == 2, "Spawn3")
			TNT1 A 0 A_JumpIf(reactiontime == 1, "Spawn2")
			ACID A 1 Bright
		Goto Spawn2
	}
}

Actor SedrinSmallExplosion {
	Renderstyle Add
	Scale 0.5
	+CLIENTSIDEONLY
	+NOINTERACTION
	States {
		Spawn:
			TNT1 A 0 NoDelay A_PlaySound("SedrinStaff/Hit")
			BPLO ABCDEFGHIJKLM 2 Bright
		Stop
	}
}

Actor SedrinTrail {
	Renderstyle Add
	Alpha 0.875
	+NOINTERACTION
	+CLIENTSIDEONLY
	States {
		Spawn:
			ACID BBBCCCDDDEEEFFF 1 Bright A_FadeOut(0.0175)
		Stop
	}
}

Actor SedrinTrail_Small : SedrinTrail {
	Scale 0.6
}