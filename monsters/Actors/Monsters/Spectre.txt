Actor Lurker : DnD_BaseHybridMonster {
	RenderStyle Add
	Obituary "%o didn't See the lurker that was shooting at him."
	HitObituary "%o didn't See the lurker right in front of him."
	Alpha 0.35
	Tag "Lurker"
	+GHOST
	Health 0x7FFFFFFF
	GibHealth -50
	PainChance 140
	Speed 12
	Radius 30
	Height 56
	Mass 450
	Species "Demon"
	+LongMeleeRange
	+DontHurtSpecies
	SeeSound "Monster/sg2sit"
	AttackSound "Monster/sg2atk"
	PainSound "demon/Pain"
	DeathSound "demon/Death"
	ActiveSound "demon/sg2act"
	+ISMONSTER
	States {
		SetInfo:
			TNT1 A 0 A_SetArg(4, DND_LURKER)
		Goto SpawnSetup
		
		Idle:
			SAR2 AB 10 A_Look
		Loop
		SeeState:
			SAR2 AABBCCDD 2 Fast A_Chase
		Goto See
		MeleeState:
			SAR2 EF 8 Fast A_FaceTarget
			SAR2 G 8 Fast A_CustomMeleeAttack(random(1, 10) * 4)
		Goto See
		MissileState:
			SAR2 GF 8 Fast A_FaceTarget
			SAR2 E 0 A_PlaySound("Weapons/bloodf")
			SAR2 E 0 A_CustomMissile("Bloodshot",22,0,-1,0,0)
			SAR2 E 8 Fast A_CustomMissile("Bloodshot",22,0,1,0,0)
		Goto See
		PainState:
			SAR2 H 2 Fast
			SAR2 H 2 Fast A_Pain
		Goto See
		DeathState:
			SAR2 I 8
			SAR2 J 8 A_Scream
			SAR2 K 4
			SAR2 L 4 A_NoBlocking
			SAR2 M 4
			SAR2 N -1
		Stop
		XDeathState:
			TNT1 A 0 A_SpawnItemEx("GenericGibber_Low", 0, 0, 0, 0, 0, 0, 0, SXF_USEBLOODCOLOR)
			SAR2 O 5
			SAR2 P 5 A_XScream
			SAR2 Q 5 A_NoBlocking
			SAR2 RSTUV 5
			SAR2 W -1
		Stop
		RaiseState:
			SAR2 NMLKJI 5
		Goto See
	}
}

Actor FlameDemonGhost : FlameDemon {
	Health 0x7FFFFFFF
	Renderstyle Add
	Alpha 0.35
	Obituary "%o was charred by the ghostly flame demon."
	+GHOST
	Speed 16
	Height 56
	Radius 30
	Mass 400
	PainChance 150
	REACTIONTIME 8
	+NORADIUSDMG
	SeeSound "demon/sight"
	PainSound "demon/pain2"
	DeathSound "demon/death2"
	ActiveSound "demon/active"
	MeleeSound "demon/melee"
	AttackSound "demon/melee"
	Translation "16:39=101:111", "40:47=5:8", "79:79=8:8", "190:191=7:8", "210:223=87:100", "224:224=161:161"
	Species "Demon"
	+DONTHURTSPECIES   
	Tag "Flame Demon"
	States {
		SetInfo:
			TNT1 A 0 A_SetArg(4, DND_FLAMEDEMON)
		Goto SpawnSetup

		Idle:
			SARG AB 8 A_Look
		Loop
		SeeState:
			SARG AABBCCDD 2 Fast A_Chase
		Goto See
		MeleeState:
			SARG EF 8 Fast A_FaceTarget
			SARG G 8 Fast A_CustomMeleeAttack(6 * random(1, 8))
		Goto See
		PainState:
			SARG H 2 Fast
			SARG H 2 Fast A_Pain
		Goto See
		XDeathState:
			TNT1 A 0 A_SpawnItemEx("GenericGibber_Low", 0, 0, 0, 0, 0, 0, 0, SXF_USEBLOODCOLOR)
		DeathState:
			SARG I 6
			SARG J 6 A_Scream
			SARG K 4
			SARG L 4 A_NoBlocking
			SARG M 4
			SARG N -1
		Stop  
		RaiseState:
			SARG NMLKJI 4
		Goto See
   }
}

ACTOR NightmareSpectre : DnD_BaseMeleeMonster {
	Health 0x7FFFFFFF
	GibHealth -50
	Speed 16
	Height 56
	Radius 30
	Mass 400
	PainChance 120
	REACTIONTIME 8
	+GHOST
	+NOINFIGHTING
	RENDERSTYLE ADD
	SeeSound "demon/sight"
	PainSound "demon/pain2"
	DeathSound "demon/death"
	ActiveSound "demon/active"
	AttackSound "demon/melee"
	HitObituary "%o was spooked by a nightmare spectre."
	Translation "16:31=199:207", "32:47=240:247", "79:79=247:247", "80:111=96:111", "190:191=246:247", "210:223=194:199"
	Species "Demon"
	Tag "Nightmare Spectre"
	+ISMONSTER
	+DONTHURTSPECIES   
	States {
		SetInfo:
			TNT1 A 0 A_SetArg(4, DND_NIGHTMAREDEMON)
		Goto SpawnSetup

		Idle:
			SARG AB 8 A_Look
		Loop
		SeeState:
			SARG AABBCCDD 2 Fast A_Chase
		Goto See
		MeleeState:
			SARG EF 6 Fast A_FaceTarget
			SARG G 6 Fast A_CustomMeleeAttack(7 * random(1, 8))
		Goto See
		PainState:
			SARG H 2 Fast
			SARG H 2 Fast A_Pain
		Goto See
		XDeathState:
			TNT1 A 0 A_SpawnItemEx("GenericGibber_Low", 0, 0, 0, 0, 0, 0, 0, SXF_USEBLOODCOLOR)
		DeathState:
			SARG I 6
			SARG J 6 A_Scream
			SARG K 4
			SARG L 4 A_NoBlocking
			SARG M 4
			SARG N -1
		Stop
		RaiseState:
			SARG NMLKJI 4
		Goto See
   }
}

Actor Gravedigger : DnD_BaseMeleeMonster {
	Translation "0:255=%[0.00,0.00,0.00]:[0.64,0.55,1.02]", "0:15=0:15", "48:78=48:78", "80:111=80:111", "160:189=160:189", "224:231=224:231"
	+ALWAYSFAST
	+GHOST
    SeeSound "demon/sight"
    PainSound "demon/pain2"
    DeathSound "demon/death"
    ActiveSound "demon/active"
    AttackSound "demon/melee"
	Alpha 0.55
	Renderstyle Add
	Obituary "%o's grave was dug up by a Gravedigger."
	Tag "Gravedigger"
	Health 0x7FFFFFFF
	GibHealth -50
	Species "Demon"
	+DONTHURTSPECIES
	PainChance 180
	Speed 10
	Radius 30
	Height 56
	Mass 400
	+ISMONSTER
	States {
		SetInfo:
			TNT1 A 0 A_SetArg(4, DND_GRAVEDIGGER)
		Goto SpawnSetup
	
		Idle:
			SARG AB 10 A_Look
		Loop
		SeeState:
			SARG AABBCCDD 2 Fast A_Chase
		Goto See
		MeleeState:
			SARG EF 8 Fast A_FaceTarget
			SARG G 8 Fast A_CustomMeleeAttack(random(1, 10) * 4, "", "")
		goto See
		PainState:
			SARG H 2 Fast
			SARG H 2 Fast A_Pain
		Goto See
		XDeathState:
			TNT1 A 0 A_SpawnItemEx("GenericGibber_Low", 0, 0, 0, 0, 0, 0, 0, SXF_USEBLOODCOLOR)
		DeathState:
			SARG I 8
			SARG J 8 A_Scream
			SARG K 4
			SARG L 4 A_NoBlocking
			SARG M 4
			SARG N -1
		Stop
		RaiseState:
			SARG N 5
			SARG MLKJI 5
		Goto See
	}
}

Actor Devourer : DnD_BaseMeleeMonster {
	Translation  "0:255=%[0.00,0.00,0.00]:[0.52,0.45,0.34]", "48:78=48:78", "80:111=80:111", "160:189=160:189", "224:235=224:235"
	+ALWAYSFAST
	+NORADIUSDMG
	+GHOST
    SeeSound "demon/sight"
    PainSound "demon/pain2"
    DeathSound "demon/death"
    ActiveSound "demon/active"
    AttackSound "demon/melee"
	Alpha 0.95
	Renderstyle Add
	Health 0x7FFFFFFF
	GibHealth -50
	Painchance 96
	Obituary "%o was devoured by a hungry Devourer."
	Tag "Devourer"
	Species "Demon"
	+DONTHURTSPECIES
	Speed 10
	Radius 30
	Height 56
	Mass 400
	+ISMONSTER
	States {
		SetInfo:
			TNT1 A 0 A_SetArg(4, DND_DEVOURER)
		Goto SpawnSetup
		
		Idle:
			SARG AB 10 A_Look
		Loop
		SeeState:
			SARG AABBCCDD 2 Fast A_Chase
		Goto See
		MeleeState:
			SARG EF 8 Fast A_FaceTarget
			SARG G 8 Fast A_CustomMeleeAttack(random(1, 10) * 4, "", "")
		goto See
		PainState:
			SARG H 2 Fast
			SARG H 2 Fast A_Pain
		Goto See
		XDeathState:
			TNT1 A 0 A_SpawnItemEx("GenericGibber_Low", 0, 0, 0, 0, 0, 0, 0, SXF_USEBLOODCOLOR)
		DeathState:
			SARG I 8
			SARG J 8 A_Scream
			SARG K 4
			SARG L 4 A_NoBlocking
			SARG M 4
			SARG N -1
		Stop
		RaiseState:
			SARG N 5
			SARG MLKJI 5
		Goto See
	}
}

Actor SoulEaterGhost : DnD_BaseHybridMonster {
	Renderstyle Add
	Alpha 0.35
	Obituary "%o had %p soul stolen by a ghost Soul Eater."
	HitObituary "%o was eaten by a ghost Soul Eater."
	+GHOST
	Health 0x7FFFFFFF
	GibHealth -50
	PainChance 128
	Speed 12
	Radius 30
	Height 56
	Mass 450
	Species "Demon"
	+LongMeleeRange
	+DontHurtSpecies
	SeeSound "SoulEater/See"
	AttackSound "Monster/sg2atk"
	PainSound "SoulEater/Pain"
	DeathSound "SoulEater/Die"
	ActiveSound "SoulEater/Act"
	Tag "Soul Eater"
	MaxTargetRange 512
	+ISMONSTER
	States {
		SetInfo:
			TNT1 A 0 A_SetArg(4, DND_SOULEATER)
		Goto SpawnSetup
		
		Idle:
			SOEA AB 10 A_Look
		Loop
		SeeState:
			SOEA AABBCCDD 2 Fast A_Chase
		Goto See
		MeleeState:
			SOEA EF 8 Fast A_FaceTarget
			TNT1 A 0 A_CustomMissile("SoulEaterProj",28)
			SOEA G 8 Fast A_CustomMeleeAttack(random(1, 10) * 4)
		Goto See
		MissileState:
			SOEA EF 8 Fast A_FaceTarget
			SOEA G 8 Fast A_CustomMissile("SoulEaterProj",28)
		Goto See
		PainState:
			SOEA H 2 Fast
			SOEA H 2 Fast A_Pain
		Goto See
		XDeathState:
			TNT1 A 0 A_SpawnItemEx("GenericGibber_Low", 0, 0, 0, 0, 0, 0, 0, SXF_USEBLOODCOLOR)
		DeathState:
			SOEA I 8
			SOEA J 8 A_Scream
			SOEA K 4
			SOEA L 4 A_NoBlocking
			SOEA M 4
			SOEA N -1
		Stop
		RaiseState:
			SOEA NMLKJI 5
		Goto See
	}
}

Actor NhumCignGhost : DnD_BaseMeleeMonster {
	Translation "0:255=%[0.00,0.00,0.00]:[1.17,0.10,1.29]", "0:47=0:47", "80:127=80:127", "152:235=152:235", "240:255=240:255"
	Health 0x7FFFFFFF
	GibHealth -50
	PainChance 96
	Speed 8
	Obituary "%o had %p ass bitten by a Nhumcign."
	Hitobituary "%o had %p ass bitten by a Nhumcign."
	SeeSound "PurpleDemon/See"
	AttackSound "Monster/sg2atk"
	PainSound "PurpleDemon/Pain"
	DeathSound "PurpleDemon/Die"
	ActiveSound "PurpleDemon/Act"
	Tag "Nhumcign"
	Species "Demon"
	Radius 30
	Height 56
	Mass 400
	RenderStyle Translucent
	Alpha 0.5
	+ISMONSTER
	+GHOST
	+DONTHURTSPECIES
	States {
		SetInfo:
			TNT1 A 0 A_SetArg(4, DND_NHUMCIGN)
		Goto SpawnSetup
	
		Idle:
			SOEA AB 10 A_Look
		Loop
		SeeState:
			TNT1 A 0 A_JumpIfInventory("NhumAngry", 1, "AngrySee")
			TNT1 A 0 A_GiveInventory("NhumHealthCheck", 1)
			SOEA AA 2 Fast A_Chase
			TNT1 A 0 A_GiveInventory("NhumHealthCheck", 1)
			SOEA B 2 Fast A_Chase
			TNT1 A 0 A_GiveInventory("DemonJumpCheck", 1)
			SOEA B 2 Fast A_Chase
			TNT1 A 0 A_JumpIfInventory("DemonGoJump", 1, "MissileGo")
			TNT1 A 0 A_GiveInventory("NhumHealthCheck", 1)
			SOEA C 2 Fast A_Chase
			TNT1 A 0 A_JumpIfInventory("NhumAngry", 1, "AngrySee")
			SOEA C 2 Fast A_Chase
			SOEA D 2 Fast A_Chase
			TNT1 A 0 A_GiveInventory("DemonJumpCheck", 1)
			SOEA D 2 Fast A_Chase
			TNT1 A 0 A_JumpIfInventory("DemonGoJump", 1, "MissileGo")
		Goto See
		AngrySee:
			SOEA AA 1 Fast A_Chase("", "", CHF_NOPLAYACTIVE)
			TNT1 A 0 A_SpawnItem("NhumWalkFXA")
			SOEA AA 1 Fast A_Chase
			TNT1 A 0 A_SpawnItem("NhumWalkFXA")
			TNT1 A 0 A_GiveInventory("DemonJumpCheck", 1)
			SOEA BB 1 Fast A_Chase("", "", CHF_NOPLAYACTIVE)
			TNT1 A 0 A_SpawnItem("NhumWalkFXB")
			SOEA BB 1 Fast A_Chase
			TNT1 A 0 A_JumpIfInventory("DemonGoJump", 1, "MissileGo")
			TNT1 A 0 A_SpawnItem("NhumWalkFXB")
			TNT1 A 0 A_GiveInventory("DemonJumpCheck", 1)
			SOEA CC 1 Fast A_Chase("", "", CHF_NOPLAYACTIVE)
			TNT1 A 0 A_SpawnItem("NhumWalkFXC")
			TNT1 A 0 A_JumpIfInventory("DemonGoJump", 1, "MissileGo")
			SOEA CC 1 Fast A_Chase
			TNT1 A 0 A_SpawnItem("NhumWalkFXC")
			SOEA DD 1 Fast A_Chase("", "", CHF_NOPLAYACTIVE)
			TNT1 A 0 A_SpawnItem("NhumWalkFXD")
			SOEA DD 1 Fast A_Chase
			TNT1 A 0 A_SpawnItem("NhumWalkFXD")
		Goto See
		MeleeState:
			SOEA E 5 Fast A_FaceTarget
			SOEA F 2 Fast A_FaceTarget
			SOEA F 3 Fast A_CustomMeleeAttack(random(1, 10) * 4)
			SOEA G 5 Fast
		Goto See
		MissileGo:
			SOEA EEEE 3 Fast A_FaceTarget
			TNT1 A 0 A_TakeInventory("DemonGoJump", 1)
			TNT1 A 0 A_PlaySound("PurpleDemon/Jump")
			TNT1 A 0 ACS_NamedExecuteAlways("DnD Monster Leap")
			SOEA F 3 Fast
			SOEA F 4 Fast ACS_NamedExecuteAlways("DnD Monster Leap Finish")
		Goto See
		PainState:
			TNT1 A 0 A_TakeInventory("DemonGoJump", 1)
			TNT1 A 0 A_GiveInventory("NhumHealthCheck", 1)
			SOEA H 2 Fast
			SOEA H 2 Fast A_Pain
		Goto See
		XdeathState:
			TNT1 A 0 A_SpawnItemEx("GenericGibber_Low", 0, 0, 0, 0, 0, 0, 0, SXF_USEBLOODCOLOR)
		DeathState:
			SOEA I 8 A_ChangeFlag("NOPAIN", 0)
			TNT1 A 0 A_TakeInventory("NhumAngry", 1)
			SOEA J 8 A_Scream
			SOEA K 4
			SOEA L 4 A_NoBlocking
			SOEA M 4
			SOEA N -1
		Stop
		RaiseState:
			SOEA NMLKJI 5
		Goto See
	}
}

Actor RavagerGhost : DnD_BaseHybridMonster {
	Health 0x7FFFFFFF
	GibHealth -50
	PainChance 96
	Speed 16
	Mass 1024
	Obituary "%o died from the toxic blood of a Ravager."
	HitObituary "%o was bitten by a Ravager."
	SeeSound "Monster/sg2sit"
	AttackSound "Monster/sg2atk"
	PainSound "demon/Pain2"
	DeathSound "demon/Death"
	ActiveSound "demon/sg2act"
	Tag "Ravager"
	Species "Demon"
	MaxTargetRange 264
	Scale 1.15
	Radius 30
	Height 56
	Translation "Ice"
	Alpha 0.5
	Renderstyle Add
	BloodColor "36 36 99"
	+ISMONSTER
	+PIERCEARMOR
	+GHOST
	States {
		SetInfo:
			TNT1 A 0 A_SetArg(4, DND_RAVAGER)
		Goto SpawnSetup

		Idle:
			SAR3 A 10 A_Look
			SAR3 A 0 A_SpawnItemEX("RavagerTrailA") 
			SAR3 B 10 A_Look
			SAR3 B 0 A_SpawnItemEX("RavagerTrailB") 
		Loop 
		SeeState: 
			SAR3 AA 2 A_Chase
			SAR3 A 0 A_SpawnItemEX("RavagerTrailA") 
			SAR3 BB 2 A_Chase
			SAR3 B 0 A_SpawnItemEX("RavagerTrailB") 
			SAR3 CC 2 A_Chase
			SAR3 C 0 A_SpawnItemEX("RavagerTrailC") 
			SAR3 DD 2 A_Chase
			SAR3 D 0 A_SpawnItemEX("RavagerTrailD") 
		Goto See
		MissileState:
			SAR3 GE 5 Fast A_FaceTarget
			SAR3 F 5 Fast A_CustomMissile("Bloodshot_Grav_Icey", 27, 0, frandom(-4, 4))
			TNT1 A 0 A_FaceTarget
			SAR3 F 5 Fast A_CustomMissile("Bloodshot_Grav_Icey", 27, 0, frandom(-4, 4))
			TNT1 A 0 A_FaceTarget
			SAR3 F 5 Fast A_CustomMissile("Bloodshot_Grav_Icey", 27, 0, frandom(-4, 4))
			TNT1 A 0 A_FaceTarget
			SAR3 F 5 Fast A_CustomMissile("Bloodshot_Grav_Icey", 27, 0, frandom(-4, 4))
			TNT1 A 0 A_FaceTarget
			SAR3 F 5 Fast A_CustomMissile("Bloodshot_Grav_Icey", 27, 0, frandom(-4, 4))
			TNT1 A 0 A_FaceTarget
			SAR3 F 5 Fast A_CustomMissile("Bloodshot_Grav_Icey", 27, 0, frandom(-4, 4))
			TNT1 A 0 A_FaceTarget
		Goto See
		Retaliate:
			SAR3 GE 5 Fast A_FaceTarget
			TNT1 A 0  A_CustomMissile("Bloodshot_Icey", 27, 0)
			SAR3 F 5 Fast A_CustomMissile("Bloodshot_Icey", 27, 0, -2)
			TNT1 A 0 A_FaceTarget
			TNT1 A 0  A_CustomMissile("Bloodshot_Icey", 27, 0)
			SAR3 F 5 Fast A_CustomMissile("Bloodshot_Icey", 27, 0, 2)
			SAR3 EG 5 Fast
		Goto See
		MeleeState:
			SAR3 EF 6 Fast A_FaceTarget
			SAR3 G 6 Fast A_CustomMeleeAttack(random(5, 10) * 4)
		Goto See
		PainState:
			SAR3 H 2 Fast
			SAR3 H 2 Fast A_Pain
			TNT1 A 0 A_Jump(128, "Retaliate")
		Goto See
		XDeathState:
			TNT1 A 0 A_SpawnItemEx("GenericGibber_Low", 0, 0, 0, 0, 0, 0, 0, SXF_USEBLOODCOLOR)
		DeathState:
			SAR3 I 8
			SAR3 J 8 A_Scream
			SAR3 K 4
			SAR3 L 4 A_NoBlocking
			SAR3 M 4
			SAR3 N -1
		Stop
		RaiseState:
			SAR3 NMLKJI 5
		Goto See
	}
}

Actor Bloodshot_Icey : Bloodshot {
	Translation "Ice"
	Alpha 0.75
	Renderstyle Add
	States {
		Spawn:
			TNT1 A 0 NoDelay ACS_NamedExecuteAlways("DnD Pierce Check")
		SpawnLoop:
			FBLD A 2 Bright A_CStaffMissileSlither
			FBLD A 0 A_SpawnItemEx("Bloodtrail_Icey",0,0,0,0,0,0,128)
			FBLD B 2 Bright A_CStaffMissileSlither
			FBLD B 0 A_SpawnItemEx("Bloodtrail_Icey",0,0,0,0,0,0,128)
		Loop
		Death:
			FBLD CDE 3 Bright
		Stop
	}
}

Actor Bloodshot_Grav_Icey : Bloodshot_Grav {
	Translation "Ice"   
	Alpha 0.75
	Renderstyle Add
	States {
		Spawn:
			TNT1 A 0 NoDelay ACS_NamedExecuteAlways("DnD Pierce Check")
			TNT1 A 0 ThrustThingZ(0, 18, 0, 0)
		SpawnLoop:
			FBLD A 2 Bright A_CStaffMissileSlither
			FBLD A 0 A_SpawnItemEx("Bloodtrail_Icey",0,0,0,0,0,0,128)
			FBLD B 2 Bright A_CStaffMissileSlither
			FBLD B 0 A_SpawnItemEx("Bloodtrail_Icey",0,0,0,0,0,0,128)
		Loop
		Death:
			FBLD CDE 3 Bright
		Stop
	}
}

Actor Bloodtrail_Icey : BloodTrail {
	Alpha 0.75
	Renderstyle Add
	Translation "Ice"
}

ACTOR RavagerTrailA {   
	Radius 1
	Height 1   
	RENDERSTYLE ADD
	Alpha 0.50
	Translation "Ice"
	Scale 1.15
	+FORCEXYBILLBOARD
	+NOINTERACTION
	+CLIENTSIDEONLY
	States {
		Spawn:
			SAR3 A 10  
			SAR3 AAAAAAAAA 3 A_FadeOut(0.05) 
		Stop 
	}
}

ACTOR RavagerTrailB : RavagerTrailA {   
	States {
		Spawn:
			SAR3 B 10  
			SAR3 BBBBBBBBB 3 A_FadeOut(0.05) 
		Stop 
	}
}

ACTOR RavagerTrailC : RavagerTrailA {   
	States {
		Spawn:
			SAR3 C 10  
			SAR3 CCCCCCCCC 3 A_FadeOut(0.05) 
		Stop 
	}
}

ACTOR RavagerTrailD : RavagerTrailA {   
	States{
		Spawn:
			SAR3 D 10  
			SAR3 DDDDDDDDD 3 A_FadeOut(0.05) 
		Stop 
	}
}